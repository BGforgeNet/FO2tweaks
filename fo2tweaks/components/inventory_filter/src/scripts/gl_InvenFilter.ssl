//#define RESERVED_ITEM00  (212)

#include "..\m_headers\define.h"
#include "..\m_headers\command.h"
#include "..\m_headers\party.h"
#include "..\m_headers\sfall\sfall.h"
#include "..\m_headers\sfall\define_extra.h"
#include "..\m_headers\sfall\dik.h"


procedure start;
procedure mouseclick;
procedure CreateInvenFilterMenu(variable loot);
procedure inven_filter_button_toggle(variable p_btn, variable spec);
procedure inven_filter_button_pressed(variable p_btn, variable filter_item_type, variable spec);
procedure filter_inventory(variable filter_item_type, variable spec);
procedure Create_btn_all(variable flag, variable trd);
procedure Create_btn_armor(variable flag, variable trd);
procedure Create_btn_weapon(variable flag, variable trd);
procedure Create_btn_ammo(variable flag, variable trd);
procedure Create_btn_drug(variable flag, variable trd);
procedure Create_btn_misc(variable flag, variable trd);
procedure Delete_btn_on;
procedure Delete_btn_off(variable p_btn, variable trd);
procedure NodeNone;
procedure IsBarter;
procedure remove_reservitem;
procedure CreateInvenFilterMenu_trader(variable loot);
procedure CreateInvenFilterMenu_trd;
procedure Delete_btn_trd_on;
procedure inven_filter_trd_button_pressed(variable p_btn, variable filter_item_type);
procedure inven_filter_trd_button_toggle(variable p_btn);
procedure filter_inventory_trader(variable filter_item_type);
procedure filter_inventory_sub(variable filter_item_type, variable spec_item, variable obj_ptr_1, variable obj_ptr_2);
procedure Add_ReservItem(variable i_count, variable obj_ptr, variable pc);
procedure check_bags_items(variable ptr);
procedure hUseSkills;
procedure Check_Party_Obj;

#define x_loc       (9)
#define y_loc       (6)
#define horiz_size  (47)  // button size
#define vertic_size (23)

#define ExcludeMisc (obj_pid(item)!=PID_FIRST_AID_KIT) and (obj_pid(item)!=PID_DOCTORS_BAG) and (obj_pid(item)!=PID_PARAMEDICS_BAG) and (obj_pid(item)!=PID_FIELD_MEDIC_KIT)
#define IncludeMisc (obj_pid(item)==PID_FIRST_AID_KIT) or (obj_pid(item)==PID_DOCTORS_BAG) or (obj_pid(item)==PID_PARAMEDICS_BAG) or (obj_pid(item)==PID_FIELD_MEDIC_KIT)

#define inven_filter_button_all     ((get_mouse_x > menu_x + x_loc and get_mouse_x < menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc and get_mouse_y < menu_y + y_loc + 21))
#define inven_filter_button_armor   ((get_mouse_x > menu_x + x_loc and get_mouse_x < menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + vertic_size and get_mouse_y < menu_y + y_loc + 21 + vertic_size))
#define inven_filter_button_weapon  ((get_mouse_x > menu_x + x_loc and get_mouse_x < menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + (vertic_size*2) and get_mouse_y < menu_y + y_loc + 21 + (vertic_size*2)))
#define inven_filter_button_ammo    ((get_mouse_x > menu_x + x_loc and get_mouse_x < menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + (vertic_size*3) and get_mouse_y < menu_y + y_loc + 21 + (vertic_size*3)))
#define inven_filter_button_drug    ((get_mouse_x > menu_x + x_loc and get_mouse_x < menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + (vertic_size*4) and get_mouse_y < menu_y + y_loc + 21 + (vertic_size*4)))
#define inven_filter_button_misc    ((get_mouse_x > menu_x + x_loc and get_mouse_x < menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + (vertic_size*5) and get_mouse_y < menu_y + y_loc + 21 + (vertic_size*5)))

#define inven_filter_trd_button_all     ((get_mouse_x > t_menu_x + x_loc and get_mouse_x < t_menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc and get_mouse_y < menu_y + y_loc + 21))
#define inven_filter_trd_button_armor   ((get_mouse_x > t_menu_x + x_loc and get_mouse_x < t_menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + vertic_size and get_mouse_y < menu_y + y_loc + 21 + vertic_size))
#define inven_filter_trd_button_weapon  ((get_mouse_x > t_menu_x + x_loc and get_mouse_x < t_menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + (vertic_size*2) and get_mouse_y < menu_y + y_loc + 21 + (vertic_size*2)))
#define inven_filter_trd_button_ammo    ((get_mouse_x > t_menu_x + x_loc and get_mouse_x < t_menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + (vertic_size*3) and get_mouse_y < menu_y + y_loc + 21 + (vertic_size*3)))
#define inven_filter_trd_button_drug    ((get_mouse_x > t_menu_x + x_loc and get_mouse_x < t_menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + (vertic_size*4) and get_mouse_y < menu_y + y_loc + 21 + (vertic_size*4)))
#define inven_filter_trd_button_misc    ((get_mouse_x > t_menu_x + x_loc and get_mouse_x < t_menu_x + x_loc + horiz_size) and (get_mouse_y > menu_y + y_loc + (vertic_size*5) and get_mouse_y < menu_y + y_loc + 21 + (vertic_size*5)))

                                        //840                                       850                                           593                                         605
#define button_exit_barter          ((get_mouse_x > (screen_width_half)+263 and get_mouse_x < (screen_width_half)+278) and (get_mouse_y > (screen_height_half)+160+shift_y and get_mouse_y < (screen_height_half)+175+shift_y))
#define button_exit_barter_check    ((x_mouse > (screen_width_half)+263 and x_mouse < (screen_width_half)+278) and (y_mouse > (screen_height_half)+160+shift_y and y_mouse < (screen_height_half)+175+shift_y))

//x790 y530  1152x864/2=576x432   790-576=214    530-432=98
#define button_exit_loot            ((get_mouse_x > (screen_width_half)+207 and get_mouse_x < (screen_width_half)+223) and (get_mouse_y > (screen_height_half)+92 and get_mouse_y < (screen_height_half)+109))
#define button_exit_loot_check      ((x_mouse > (screen_width_half)+207 and x_mouse < (screen_width_half)+223) and (y_mouse > (screen_height_half)+92 and y_mouse < (screen_height_half)+109))

variable screen_width_half,screen_height_half;
variable x_mouse,y_mouse;

variable menu_x;
variable menu_y, shift_y;
variable btn_all,btn_armor,btn_weapon,btn_ammo,btn_drug,btn_misc;
variable inventory_once;
variable pressed_once;
variable special;
variable critter;
variable dude_in_barter;
variable reserv_item_ptr_1,reserv_item_ptr_2,reserv_item_ptr_3,reserv_item_ptr_4;

//for trader
variable t_menu_x;
variable btn_trd_all,btn_trd_armor,btn_trd_weapon,btn_trd_ammo,btn_trd_drug,btn_trd_misc;
variable trader_ptr;
variable trd_inventory,critter_tbox,dude_enter_barter;
variable reserv_item_trd_ptr_1,reserv_item_trd_ptr_2,reserv_item_trd_ptr_3,reserv_item_trd_ptr_4;

//for all container
//#define Container_Use          get_sfall_global_int("CNTR_USE")
//#define Container_Use_off      set_sfall_global("CNTR_USE",0)

//for trunk
variable CNTR_USE, CNTR_PTR;
variable object_array;
variable used, uSkill;

variable trunk_filter,trunk_use;
variable trunk_ptr;
variable reserv_item_tnk_ptr_1,reserv_item_tnk_ptr_2,reserv_item_tnk_ptr_3;
variable reserv_item_tnk_ptr_4,reserv_item_tnk_ptr_5,reserv_item_tnk_ptr_6;



procedure start begin

variable m, objtype;
//  variable counter, item, pid;

  if game_loaded then begin
    register_hook_proc(HOOK_MOUSECLICK, MouseClick);
    register_hook_proc(HOOK_BARTERPRICE, IsBarter);
    register_hook_proc(HOOK_USESKILL, hUseSkills);
    set_global_script_type(1);
    set_global_script_repeat(1);
    object_array:=create_array_map;
    screen_width_half:=get_screen_width/2;
    screen_height_half:=get_screen_height/2;
    btn_trd_all:=1;
    if get_screen_height == 480 then shift_y:=50;
// test items
// counter:=50;
//   item_caps_adjust(dude_obj, 1000);
//   while counter > 0 do begin
//      pid:=random(1,618);
//      if get_proto_data(pid, PROTO_IT_TYPE) != item_type_container then begin
//         item:=create_object(pid, 0, 0);
//         add_mult_objs_to_inven(dude_obj, item, 1);
//         counter-=1;
//      end
//   end
  end else begin

//Code for trunk (old)
//if party_member_obj(PID_CAR_TRUNK)!=0 then begin
//   if Car_Trunk_Use!=0 and not(anim_busy(dude_obj)) then begin
//       if Car_Trunk_Use==dude_tile then begin
//           trunk_filter:=1;
//           trunk_use:=1;
//           dude_in_barter:=1; //also for Trunk
//       end
//       Car_Trunk_Use_off;
//   end
//end

          if not(used) and anim_busy(dude_obj) and art_anim(obj_art_fid(dude_obj))==11 then begin
              CNTR_USE:=dude_tile;
              used:=true;
              uSkill:=false;
          end else if used==true then begin
//              debug_msg("loop 0");
              if CNTR_USE==dude_tile and not(anim_busy(dude_obj)) and not(uSkill) then begin
//              debug_msg("loop 1");
                object_array:=tile_get_objs(tile_num_in_direction(dude_tile, dude_cur_rot, 1), dude_elevation);
                m:=0;
                while objtype!=1 and object_array[m] > 0 do begin
//                      debug_msg("value: "+object_array[m]);
//                      debug_msg("pid: "+obj_pid(object_array[m]));
//                      debug_msg("type: "+obj_item_subtype(object_array[m]));
                      objtype:=obj_item_subtype(object_array[m]);
                      m+=1;
                end
                if objtype==1 then begin
                    if obj_pid(object_array[m-1])==PID_CAR_TRUNK then begin
                          CNTR_PTR:=object_array[m-1];
                          CNTR_USE:=-1;
                          trunk_filter:=1;
                          trunk_use:=1;
                          dude_in_barter:=1; //also for Trunk
                    end else begin
                          CNTR_PTR:=0;
                          CNTR_USE:=0;
                    end
                end else begin
                    CNTR_PTR:=Check_Party_Obj;
                    if CNTR_PTR!=(-1) then begin
                       CNTR_USE:=-1;
                       trunk_filter:=1;
                       trunk_use:=1;
                       dude_in_barter:=1; //also for Trunk
                    end else CNTR_PTR:=0;
                end
                objtype:=0;
                used:=false;
              end else if CNTR_USE!=dude_tile then used:=false;
              if uSkill then used:=false;
          end

      if dude_in_barter and key_pressed(DIK_ESCAPE) then begin
         dude_in_barter:=false;
         trunk_filter:=0;
      end
      if (get_game_mode BWAND INVENTORY) > 0 or ((get_game_mode BWAND DIALOG) > 0 and dude_in_barter) or trunk_filter then begin
         if key_pressed(DIK_LCONTROL) and (dude_in_barter) and not(trd_inventory) then begin
             if btn_trd_all then call CreateInvenFilterMenu_trader(trunk_filter); else call CreateInvenFilterMenu_trd;
             trd_inventory:=1;
         end else if (dude_in_barter) and trd_inventory and not(key_pressed(DIK_LCONTROL)) then begin
             deleteWin("inven_filter_trd");
             trd_inventory:=0;
         end
         //inventory on
         if inventory_once == 0 then begin
            inventory_once:=1;
            critter:=create_object(PID_MELEE_THUG_MALE, 1,2);
            call CreateInvenFilterMenu(trunk_filter);
            if trunk_filter then begin
               trader_ptr:=create_object(PID_MELEE_THUG_MALE, 1,2);
               trunk_ptr:=CNTR_PTR;  //trunk_ptr:=party_member_obj(PID_CAR_TRUNK);
            end
            if dude_in_barter then begin
               critter_tbox:=create_object(PID_MELEE_THUG_MALE, 1,2);
               //debug_msg("data: "+get_proto_data(RESERVED_ITEM00, 124));
               set_proto_data(RESERVED_ITEM00, 124, -1); // (get_proto_data(RESERVED_ITEM00, 124) bwAND 0xFFFF0000)
               set_proto_data(RESERVED_ITEM00, PROTO_IT_WEIGHT, 0);
            end
         end
      end else if inventory_once == 1 then begin
         //inventory off
         deleteWin("inven_filter");
         inventory_once:=0;
         move_obj_inven_to_obj(critter, dude_obj);
         destroy_object(critter);
         if dude_enter_barter or trunk_use then begin
            if trunk_use then begin
                trunk_use:=0;
                move_obj_inven_to_obj(critter_tbox, trunk_ptr);
                destroy_object(trader_ptr);
            end else move_obj_inven_to_obj(critter_tbox, trader_ptr);
            destroy_object(critter_tbox);
            deleteWin("inven_filter_back");
            if trd_inventory then deleteWin("inven_filter_trd");
            trd_inventory:=0;
            btn_trd_all:=1;
            dude_enter_barter:=false;
            dude_in_barter:=0;
         end
         call Remove_ReservItem;
      end
  end
end

procedure MouseClick begin

   variable type:=get_sfall_arg;     //event type: 1 - pressed, 0 - released
   variable button:=get_sfall_arg;   //button number (0 - left, 1 - right, up to 7)

   if (get_game_mode BWAND INVENTORY) > 0 or (get_game_mode BWAND DIALOG and dude_in_barter) > 0 or trunk_filter then begin
      if dude_in_barter or trunk_filter then call Remove_ReservItem;
      if button == 0 then begin
         if type == 1 then begin
             x_mouse:=get_mouse_x;
             y_mouse:=get_mouse_y;
             //debug_msg("X:"+get_mouse_x);
             //debug_msg("Y:"+get_mouse_y);
         end
         if button_exit_barter and dude_in_barter and type==0 and not(trunk_filter) and button_exit_barter_check then dude_in_barter:=false;
         if button_exit_loot and trunk_filter and type==0 and button_exit_loot_check then trunk_filter:=false;

         if inven_filter_button_all and not(btn_all) and not(trd_inventory) then begin //button all no filter
            call inven_filter_button_pressed("all", -1,0);
         end else if inven_filter_button_armor and not(btn_armor) and not(trd_inventory) then begin //button armor
            call inven_filter_button_pressed("armor", item_type_armor,0);
         end else if inven_filter_button_weapon and not(btn_weapon) and not(trd_inventory) then begin //button weapon
            call inven_filter_button_pressed("weapon", item_type_weapon,0);
         end else if inven_filter_button_ammo and not(btn_ammo) and not(trd_inventory) then begin //button ammo
            call inven_filter_button_pressed("ammo", item_type_ammo,0);
         end else if inven_filter_button_drug and not(btn_drug) and not(trd_inventory) then begin //button drug
            call inven_filter_button_pressed("drug", item_type_drug,0);
         end else if inven_filter_button_misc and not(btn_misc) and not(trd_inventory) then begin //button mis
            call inven_filter_button_pressed("misc", item_type_misc_item,0);
            //for trader/trunk
         end else if inven_filter_trd_button_all and not(btn_trd_all) and trd_inventory then begin //button all no filter
            call inven_filter_trd_button_pressed("all", -1);
         end else if inven_filter_trd_button_armor and not(btn_trd_armor) and trd_inventory then begin //button armor
            call inven_filter_trd_button_pressed("armor", item_type_armor);
         end else if inven_filter_trd_button_weapon and not(btn_trd_weapon) and trd_inventory then begin //button weapon
            call inven_filter_trd_button_pressed("weapon", item_type_weapon);
         end else if inven_filter_trd_button_ammo and not(btn_trd_ammo) and trd_inventory then begin //button ammo
            call inven_filter_trd_button_pressed("ammo", item_type_ammo);
         end else if inven_filter_trd_button_drug and not(btn_trd_drug) and trd_inventory then begin //button drug
            call inven_filter_trd_button_pressed("drug", item_type_drug);
         end else if inven_filter_trd_button_misc and not(btn_trd_misc) and trd_inventory then begin //button mis
            call inven_filter_trd_button_pressed("misc", item_type_misc_item);
         end //else
         if pressed_once and type==0 then pressed_once:=0;
         // клик средней кнопкой мышки по кнопкам патроны или оружие
      end else if button == 2 and (btn_ammo or btn_weapon) then  begin
          if inven_filter_button_ammo and not(btn_ammo) then begin //button ammo
            call inven_filter_button_pressed("ammo", item_type_ammo,1);
          end else if inven_filter_button_weapon and not(btn_weapon) then begin //button weapon
            call inven_filter_button_pressed("weapon", item_type_weapon,1);
          end else if pressed_once and type==0 then pressed_once:=0;
      end
   end

end

procedure CreateInvenFilterMenu(variable loot) begin
// параметры окна
  if (get_game_mode BWAND DIALOG) > 0 then begin
    menu_x:=(screen_width_half)-320;  //-384   //-320 for olymp
    menu_y:=(screen_height_half)-150+shift_y; //+40   //-150  for olymp
  end else if loot then begin //for Loot
    menu_x:=(screen_width_half)-330;
    if menu_x < 0 then menu_x:=0;
    menu_y:=(screen_height_half)-238;
  end else begin //for PC inventar
    menu_x:=(screen_width_half)-312;
    menu_y:=(screen_height_half)-238;
  end
  
  if (get_game_mode BWAND DIALOG) > 0 or loot then begin
    if loot then t_menu_x:=(screen_width_half)+269; else t_menu_x:=(screen_width_half)+255;
    createWin("inven_filter_back", t_menu_x, menu_y, 63, 150);
    selectWin("inven_filter_back");
    display("PCX\\inv_filter\\main_form.pcx");
    showWin;
  end
  
//создание окна
  createWin("inven_filter", menu_x, menu_y, 63, 150);
  selectWin("inven_filter");
  display("PCX\\inv_filter\\main_form.pcx");
  call Create_btn_all("on",0);
  call Create_btn_armor("off",0);
  call Create_btn_weapon("off",0);
  call Create_btn_ammo("off",0);
  call Create_btn_drug("off",0);
  call Create_btn_misc("off",0);
  showWin;

   btn_all:=1;
   btn_armor:=0;
   btn_weapon:=0;
   btn_ammo:=0;
   btn_drug:=0;
   btn_misc:=0;
   special:=0;
   
end

procedure CreateInvenFilterMenu_trader(variable loot) begin
// параметры окна                 for Loot                      for Barter
  if loot then t_menu_x:=(screen_width_half)+269; else t_menu_x:=(screen_width_half)+255;

//создание окна
  createWin("inven_filter_trd", t_menu_x, menu_y, 63, 150);
  selectWin("inven_filter_trd");
  display("PCX\\inv_filter\\main_form.pcx");
  call Create_btn_all("on",1);
  call Create_btn_armor("off",1);
  call Create_btn_weapon("off",1);
  call Create_btn_ammo("off",1);
  call Create_btn_drug("off",1);
  call Create_btn_misc("off",1);
  showWin;
  
   btn_trd_all:=1;
   btn_trd_armor:=0;
   btn_trd_weapon:=0;
   btn_trd_ammo:=0;
   btn_trd_drug:=0;
   btn_trd_misc:=0;
   
end

procedure CreateInvenFilterMenu_trd begin

//создание окна
  createWin("inven_filter_trd", t_menu_x, menu_y, 63, 150);
  selectWin("inven_filter_trd");
  display("PCX\\inv_filter\\main_form.pcx");

  if not(btn_trd_all) then call Create_btn_all("off",1);
  if not(btn_trd_armor) then call Create_btn_armor("off",1);
  if not(btn_trd_weapon) then call Create_btn_weapon("off",1);
  if not(btn_trd_ammo) then call Create_btn_ammo("off",1);
  if not(btn_trd_drug) then call Create_btn_drug("off",1);
  if not(btn_trd_misc) then call Create_btn_misc("off",1);

  // создать новую кнопку которая была нажата.
  if btn_trd_all then call Create_btn_all("on",1);
  else if btn_trd_armor then call Create_btn_armor("on",1);
  else if btn_trd_weapon then call Create_btn_weapon("on",1);
  else if btn_trd_ammo then call Create_btn_ammo("on",1);
  else if btn_trd_drug then call Create_btn_drug("on",1);
  else if btn_trd_misc then call Create_btn_misc("on",1);
  
  showWin;
  
end

procedure inven_filter_button_pressed(variable p_btn, variable filter_item_type, variable spec) begin
variable total;
//get_ini_string("ddraw.ini|Main|TranslationsINI");
      
 if not(pressed_once) then begin
    pressed_once:=1;
    play_sfx("IB2P1XX1");

    total:=check_bags_items(dude_obj);
    if total>1 or (dude_in_barter and total) then begin
      //"You have exceeded the number of item of type container."
      display_msg(get_ini_string("./data/PCX/inv_filter/InvenFilter.ini|InventoryFilter|MsgI"));
       //"Remove excess:"
      if not(dude_in_barter) or trunk_filter then create_message_window(get_ini_string("./data/PCX/inv_filter/InvenFilter.ini|InventoryFilter|MsgW"));
   end else begin
      call inven_filter_button_toggle(p_btn, spec);  //button down so highlight!
      call filter_inventory(filter_item_type, spec);
    end
    
 end

end

procedure inven_filter_trd_button_pressed(variable p_btn, variable filter_item_type) begin

if not(pressed_once) then begin
    pressed_once:=1;
    play_sfx("IB2P1XX1");

    // перемещаем инвентарь контейнера в инвентарь криттера
    if trunk_filter then move_obj_inven_to_obj(trunk_ptr, trader_ptr);

    if check_bags_items(trader_ptr) >=1 then begin
        if trunk_filter then begin
            display_msg(get_ini_string("./data/PCX/inv_filter/InvenFilter.ini|InventoryFilter|MsgC"));
            create_message_window(get_ini_string("./data/PCX/inv_filter/InvenFilter.ini|InventoryFilter|MsgW"));
        end  else display_msg(get_ini_string("./data/PCX/inv_filter/InvenFilter.ini|InventoryFilter|MsgT"));
    end else begin
      call inven_filter_trd_button_toggle(p_btn);  //button down so highlight!
      call filter_inventory_trader(filter_item_type);
    end
    
    //возвращаем инвентарь криттера в контейнер (дублирование)
    if trunk_filter then move_obj_inven_to_obj(trader_ptr, trunk_ptr);

end
   
end

procedure inven_filter_button_toggle(variable p_btn, variable spec) begin

   selectWin("inven_filter");
if not((spec) and (btn_ammo or btn_weapon)) then begin
   // удалить предыдущую нажатую кнопку
   call Delete_btn_on;
   //создать на месте удаленноей кнопки новую кнопку (закрыть дыру)
   if btn_all then begin
     call Create_btn_all("off",0);
   end else if btn_armor then begin
      call Create_btn_armor("off",0);
   end else if btn_weapon then begin
      call Create_btn_weapon("off",0);
   end else if btn_ammo then begin
      call Create_btn_ammo("off",0);
   end else if btn_drug then begin
      call Create_btn_drug("off",0);
   end else if btn_misc then begin
      call Create_btn_misc("off",0);
   end
   //debug_msg("Create off: OK");

   if special then begin
      call Delete_btn_on;
      call Create_btn_ammo("off",0);
      special:=0;
   end
end else special:=1;

   //удалить кнопку которая была нажата
   call Delete_btn_off(p_btn,0);
   // создать новую кнопку которая была нажата.
   if p_btn=="all" then call Create_btn_all("on",0);
   else if p_btn=="armor" then call Create_btn_armor("on",0);
   else if p_btn=="weapon" then call Create_btn_weapon("on",0);
   else if p_btn=="ammo" then call Create_btn_ammo("on",0);
   else if p_btn=="drug" then call Create_btn_drug("on",0);
   else if p_btn=="misc" then call Create_btn_misc("on",0);
   
//   debug_msg("btn_all: "+ btn_all);
//   debug_msg("btn_armor: "+ btn_armor);
//   debug_msg("btn_weapon:" + btn_weapon);
//   debug_msg("btn_ammo:" + btn_ammo);
//   debug_msg("btn_drug:" + btn_drug);
//   debug_msg("btn_misc:" + btn_misc);
//   debug_msg("-----end------");

  showWin;


end

procedure inven_filter_trd_button_toggle(variable p_btn) begin

   selectWin("inven_filter_trd");
   // удалить предыдущую нажатую кнопку
   call Delete_btn_trd_on;
   //создать на месте удаленноей кнопки новую кнопку (закрыть дыру)
   if btn_trd_all then begin
     call Create_btn_all("off",1);
   end else if btn_trd_armor then begin
      call Create_btn_armor("off",1);
   end else if btn_trd_weapon then begin
      call Create_btn_weapon("off",1);
   end else if btn_trd_ammo then begin
      call Create_btn_ammo("off",1);
   end else if btn_trd_drug then begin
      call Create_btn_drug("off",1);
   end else if btn_trd_misc then begin
      call Create_btn_misc("off",1);
   end
   //debug_msg("Create off: OK");

   //удалить кнопку которая была нажата
   call Delete_btn_off(p_btn,1);
   // создать новую кнопку которая была нажата.
   if p_btn=="all" then call Create_btn_all("on",1);
   else if p_btn=="armor" then call Create_btn_armor("on",1);
   else if p_btn=="weapon" then call Create_btn_weapon("on",1);
   else if p_btn=="ammo" then call Create_btn_ammo("on",1);
   else if p_btn=="drug" then call Create_btn_drug("on",1);
   else if p_btn=="misc" then call Create_btn_misc("on",1);
   
  showWin;

end

procedure filter_inventory(variable filter_item_type, variable spec) begin
variable item_counter,item,items,spec_item,item_count,need_item_count:=3;
variable itembag,itembags;
variable itembag2,itembags2;
variable itempack,itempacks;

//debug_msg("mode:"+get_game_mode);

  if trunk_filter then need_item_count:=6;

  spec_item:=7;
  if spec then if filter_item_type==item_type_ammo then spec_item:=item_type_weapon; else spec_item:=item_type_ammo;
  
   move_obj_inven_to_obj(critter, dude_obj);
   item_count:=inven_count(dude_obj);

   if not(dude_in_barter) then begin
      //Удаляем сумку из инвентаря что-бы избежать бага.
      itembag:=obj_carrying_pid_obj(dude_obj, PID_BAG);
      if itembag!=0 and obj_item_subtype(itembag)==item_type_container then itembags:=rm_mult_objs_from_inven(dude_obj, itembag, obj_is_carrying_obj_pid(dude_obj, obj_pid(itembag)));
      //debug_msg("bag_DEL!! " + itembag + " " + itembags);
      itembag2:=obj_carrying_pid_obj(dude_obj, PID_BROWN_BAG);
      if itembag2!=0 and obj_item_subtype(itembag2)==item_type_container then itembags2:=rm_mult_objs_from_inven(dude_obj, itembag2, obj_is_carrying_obj_pid(dude_obj, obj_pid(itembag2)));
      //debug_msg("bag_DEL!! " + itembag2 + " " + itembags2);
      itempack:=obj_carrying_pid_obj(dude_obj, PID_BACKPACK);
      if itempack!=0 and obj_item_subtype(itempack)==item_type_container then itempacks:=rm_mult_objs_from_inven(dude_obj, itempack, obj_is_carrying_obj_pid(dude_obj, obj_pid(itempack)));
      //debug_msg("pack_DEL!! " + itempacks);
   end
   
   item_counter:=filter_inventory_sub(filter_item_type,spec_item,dude_obj,critter);

   if not(dude_in_barter) then begin
      if itembags!=0 then add_mult_objs_to_inven(dude_obj, itembag, itembags);
      if itembags2!=0 then add_mult_objs_to_inven(dude_obj, itembag2, itembags2);
      if itempacks!=0 then add_mult_objs_to_inven(dude_obj, itempack, itempacks);
   end
   
   //показывать деньги сверху в режиме бартера
   if (get_game_mode==DIALOG) and filter_item_type!=(-1) then begin
      item:=obj_carrying_pid_obj(dude_obj, PID_BOTTLE_CAPS);
      if item!=0 then begin
          items:=rm_mult_objs_from_inven(dude_obj, item, obj_is_carrying_obj_pid(dude_obj, obj_pid(item)));
          add_mult_objs_to_inven(dude_obj, item, items);
      end
   end
  
   if dude_in_barter then begin
       if item_counter <= need_item_count then call Add_ReservItem(item_counter,dude_obj,1);

       //display_msg("count:"+item_count);
       tap_key(DIK_DOWNARROW);
       while item_count > 0 do begin
          tap_key(DIK_UPARROW);
          item_count-=1;
       end
   end else tap_key(DIK_HOME);

end

procedure filter_inventory_trader(variable filter_item_type) begin
variable item_counter,item,items,item_count,need_item_count:=3;
variable itembag,itembags;
variable itembag2,itembags2;
variable itempack,itempacks;

//debug_msg("mode:"+get_game_mode);

    if trunk_filter then need_item_count:=6;
   
    move_obj_inven_to_obj(critter_tbox, trader_ptr);
    item_count:=inven_count(trader_ptr);

//Удаляем сумки из инвентаря что-бы избежать бага.
//    itembag:=obj_carrying_pid_obj(trader_ptr, PID_BAG);
//    if itembag!=0 and obj_item_subtype(itembag)==item_type_container then itembags:=rm_mult_objs_from_inven(trader_ptr, itembag, obj_is_carrying_obj_pid(trader_ptr, obj_pid(itembag)));
//    itembag2:=obj_carrying_pid_obj(trader_ptr, PID_BROWN_BAG);
//    if itembag2!=0 and obj_item_subtype(itembag2)==item_type_container then itembags2:=rm_mult_objs_from_inven(trader_ptr, itembag2, obj_is_carrying_obj_pid(trader_ptr, obj_pid(itembag2)));
//    itempack:=obj_carrying_pid_obj(trader_ptr, PID_BACKPACK);
//    if itempack!=0 and obj_item_subtype(itempack)==item_type_container then itempacks:=rm_mult_objs_from_inven(trader_ptr, itempack, obj_is_carrying_obj_pid(trader_ptr, obj_pid(itempack)));
   
    item_counter:=filter_inventory_sub(filter_item_type,-1,trader_ptr,critter_tbox);

//    if itembags!=0 then add_mult_objs_to_inven(trader_ptr, itembag, itembags);
//    if itembags2!=0 then add_mult_objs_to_inven(trader_ptr, itembag2, itembags2);
//    if itempacks!=0 then add_mult_objs_to_inven(trader_ptr, itempack, itempacks);

     //показывать деньги сверху в режиме бартера
     if (get_game_mode==DIALOG) and filter_item_type!=(-1) then begin
        item:=obj_carrying_pid_obj(trader_ptr, PID_BOTTLE_CAPS);
        if item!=0 then begin
            items:=rm_mult_objs_from_inven(trader_ptr, item, obj_is_carrying_obj_pid(trader_ptr, obj_pid(item)));
            add_mult_objs_to_inven(trader_ptr, item, items);
        end
     end

     if item_counter <= need_item_count then call Add_ReservItem(item_counter,trader_ptr,0);

     //возвращаем инвентарь в контейнер
     if trunk_filter then move_obj_inven_to_obj(trader_ptr, trunk_ptr);

     //debug_msg("count:"+item_count);
     tap_key(DIK_DOWNARROW);
     while item_count > 0 do begin
        tap_key(DIK_UPARROW);
        item_count-=1;
     end

end

procedure filter_inventory_sub(variable filter_item_type, variable spec_item, variable obj_ptr_1, variable obj_ptr_2) begin
variable item,items,item_counter:=0;

 if filter_item_type!=(-1) then begin
   while inven_ptr(obj_ptr_1, item_counter) > 0 and item_counter < 500 do begin
     item:=inven_ptr(obj_ptr_1, item_counter);
//     debug_msg("item:"+obj_name(item));
//     debug_msg("counter" + item_counter);
     //если предмет деньги то остаются в инвентаре игрока
     if (obj_pid(item)!=PID_BOTTLE_CAPS or (get_game_mode BWAND INVENTORY) > 0 or trunk_filter) then begin
      if filter_item_type < item_type_misc_item then begin
         if filter_item_type == item_type_drug then begin
            //debug_msg("drug item:"+obj_pid(item));
            //не исключаются drugs и некоторые придметы относящиеся к этому типу
            if obj_item_subtype(item)!=item_type_drug and ExcludeMisc then begin
              items:=rm_mult_objs_from_inven(obj_ptr_1, item, obj_is_carrying_obj_pid(obj_ptr_1, obj_pid(item))); //obj_is_carrying_obj(obj_ptr_1, item)
              add_mult_objs_to_inven(obj_ptr_2, item, items);
            end else item_counter+=1;
         end else begin
            //не исключаются предметы тип которых соответствует фильтру
            if obj_item_subtype(item) != filter_item_type and obj_item_subtype(item)!=spec_item then begin
               //debug_msg("remove item:"+obj_name(item));
               items:=rm_mult_objs_from_inven(obj_ptr_1, item, obj_is_carrying_obj_pid(obj_ptr_1, obj_pid(item))); //obj_is_carrying_obj(obj_ptr_1, item)
               add_mult_objs_to_inven(obj_ptr_2, item, items);
               //debug_msg("item DEL!! " + item_counter);
            end else item_counter+=1;
         end
      end else begin //не исключаются misc и key
         if obj_item_subtype(item)!=item_type_misc_item and obj_item_subtype(item)!=item_type_key_item or IncludeMisc then begin
           items:=rm_mult_objs_from_inven(obj_ptr_1, item, obj_is_carrying_obj_pid(obj_ptr_1, obj_pid(item))); //obj_is_carrying_obj(obj_ptr_1, item)
           add_mult_objs_to_inven(obj_ptr_2, item, items);
         end else item_counter+=1;
      end
     end else begin
        item_counter+=1;
        //debug_msg("inc counter:"+item_counter);
     end
   end
 end else begin
    //показывать деньги сверху при выборе All
    item:=obj_carrying_pid_obj(obj_ptr_1, PID_BOTTLE_CAPS);
    if item!=0 then begin
        items:=rm_mult_objs_from_inven(obj_ptr_1, item, obj_is_carrying_obj_pid(obj_ptr_1, obj_pid(item)));
        add_mult_objs_to_inven(obj_ptr_1, item, items);
    end
    item_counter:=inven_count(obj_ptr_1);
 end
 //debug_msg("FILTER_DONE!!");

 return item_counter;

end

procedure Delete_btn_on begin
//debug_msg("Delete... " );
   if btn_all then begin
      deleteButton("filter_all_on0");
   end else if btn_armor then begin
      deleteButton("filter_armor_on0");
   end else if btn_weapon then begin
       deleteButton("filter_weapon_on0");
   end else if btn_ammo then begin
      deleteButton("filter_ammo_on0");
   end else if btn_drug then begin
      deleteButton("filter_drug_on0");
   end else if btn_misc then begin
      deleteButton("filter_misc_on0");
   end
//debug_msg("delete:OK");
end

procedure Delete_btn_trd_on begin
//debug_msg("Delete... " );
   if btn_trd_all then begin
      deleteButton("filter_all_on1");
   end else if btn_trd_armor then begin
      deleteButton("filter_armor_on1");
   end else if btn_trd_weapon then begin
       deleteButton("filter_weapon_on1");
   end else if btn_trd_ammo then begin
      deleteButton("filter_ammo_on1");
   end else if btn_trd_drug then begin
      deleteButton("filter_drug_on1");
   end else if btn_trd_misc then begin
      deleteButton("filter_misc_on1");
   end
//debug_msg("delete:OK");
end

procedure Delete_btn_off(variable btn, variable trd) begin
//debug_msg("Delete OFF... " );
   if btn=="all" then begin
      deleteButton("filter_all_off"+trd);
   end else if btn=="armor" then begin
      deleteButton("filter_armor_off"+trd);
   end else if btn=="weapon" then begin
       deleteButton("filter_weapon_off"+trd);
   end else if btn=="ammo" then begin
      deleteButton("filter_ammo_off"+trd);
   end else if btn=="drug" then begin
      deleteButton("filter_drug_off"+trd);
   end else if btn=="misc" then begin
      deleteButton("filter_misc_off"+trd);
   end
//debug_msg("delete:OK");
end

procedure Create_btn_all(variable flag, variable trd) begin
  addButton("filter_all_" + flag+trd, x_loc, 1+y_loc, 47, 20);
  addButtonGfx("filter_all_" + flag+trd, "PCX\\inv_filter\\btn_all_on.pcx","PCX\\inv_filter\\btn_all_" + flag + ".pcx", "PCX\\inv_filter\\btn_all_hover_" + flag + ".pcx");
  addButtonProc("filter_all_" + flag+trd, NodeNone, NodeNone, NodeNone, NodeNone);  //inven buttons don't work

  if not(trd) then begin
      if flag=="on" then btn_all:=1; else btn_all:=0;
  end else if flag=="on" then btn_trd_all:=1; else btn_trd_all:=0;

end

procedure Create_btn_armor(variable flag, variable trd) begin
  addButton("filter_armor_" + flag+trd, x_loc, 1+y_loc+vertic_size, 47, 20);
  addButtonGfx("filter_armor_" + flag+trd, "PCX\\inv_filter\\btn_armor_on.pcx","PCX\\inv_filter\\btn_armor_" + flag + ".pcx", "PCX\\inv_filter\\btn_armor_hover_" + flag + ".pcx");
  addButtonProc("filter_armor_" + flag+trd, NodeNone, NodeNone, NodeNone, NodeNone);  //inven buttons don't work

  if not(trd) then begin
     if flag=="on" then btn_armor:=1; else btn_armor:=0;
  end else if flag=="on" then btn_trd_armor:=1; else btn_trd_armor:=0;
  
end

procedure Create_btn_weapon(variable flag, variable trd) begin
  addButton("filter_weapon_" + flag+trd, x_loc, 1+y_loc+(vertic_size*2), 47, 20);
  addButtonGfx("filter_weapon_" + flag+trd, "PCX\\inv_filter\\btn_weapon_on.pcx","PCX\\inv_filter\\btn_weapon_" + flag + ".pcx", "PCX\\inv_filter\\btn_weapon_hover_" + flag + ".pcx");
  addButtonProc("filter_weapon_" + flag+trd, NodeNone, NodeNone, NodeNone, NodeNone);  //inven buttons don't work

  if not(trd) then begin
      if flag=="on" then btn_weapon:=1; else btn_weapon:=0;
  end else if flag=="on" then btn_trd_weapon:=1; else btn_trd_weapon:=0;
  
end

procedure Create_btn_ammo(variable flag, variable trd) begin
  addButton("filter_ammo_" + flag+trd, x_loc, 1+y_loc+(vertic_size*3), 47, 20);
  addButtonGfx("filter_ammo_" + flag+trd, "PCX\\inv_filter\\btn_ammo_on.pcx","PCX\\inv_filter\\btn_ammo_" + flag + ".pcx", "PCX\\inv_filter\\btn_ammo_hover_" + flag + ".pcx");
  addButtonProc("filter_ammo_" + flag+trd, NodeNone, NodeNone, NodeNone, NodeNone);  //inven buttons don't work

  if not(trd) then begin
      if flag=="on" then btn_ammo:=1; else btn_ammo:=0;
  end else if flag=="on" then btn_trd_ammo:=1; else btn_trd_ammo:=0;

end

procedure Create_btn_drug(variable flag, variable trd) begin
  addButton("filter_drug_" + flag+trd, x_loc, 1+y_loc+(vertic_size*4), 47, 20);
  addButtonGfx("filter_drug_" + flag+trd, "PCX\\inv_filter\\btn_drug_on.pcx","PCX\\inv_filter\\btn_drug_" + flag + ".pcx", "PCX\\inv_filter\\btn_drug_hover_" + flag + ".pcx");
  addButtonProc("filter_drug_" + flag+trd, NodeNone, NodeNone, NodeNone, NodeNone);  //inven buttons don't work

  if not(trd) then begin
      if flag=="on" then btn_drug:=1; else btn_drug:=0;
  end else if flag=="on" then btn_trd_drug:=1; else btn_trd_drug:=0;

end

procedure Create_btn_misc(variable flag, variable trd) begin
  addButton("filter_misc_" + flag+trd, x_loc, 1+y_loc+(vertic_size*5), 47, 20);
  addButtonGfx("filter_misc_" + flag+trd, "PCX\\inv_filter\\btn_misc_on.pcx", "PCX\\inv_filter\\btn_misc_" + flag + ".pcx", "PCX\\inv_filter\\btn_misc_hover_" + flag + ".pcx");
  addButtonProc("filter_misc_" + flag+trd, NodeNone, NodeNone, NodeNone, NodeNone);  //inven buttons don't work

  if not(trd) then begin
      if flag=="on" then btn_misc:=1; else btn_misc:=0;
  end else if flag=="on" then btn_trd_misc:=1; else btn_trd_misc:=0;
  
end

procedure Add_ReservItem(variable i_count, variable obj_ptr, variable pc) begin
/****************** Код для перересовки итемов в слоте ******************/
variable crtr, item_ptr,items;
     //добавить фантом предметы в инвентарь
     //debug_msg("count:"+i_count);

        crtr:=create_object(PID_MELEE_THUG_MALE, 1,2);

        if pc then begin
        // перемещаем инвентарь dude в инвентарь временного криттера (решение бага с надетой броней)
           while inven_ptr(dude_obj, 0) > 0 do begin
              item_ptr:=inven_ptr(dude_obj, 0);
              items:=rm_mult_objs_from_inven(dude_obj, item_ptr, obj_is_carrying_obj_pid(dude_obj, obj_pid(item_ptr))); //obj_is_carrying_obj(obj_ptr_1, item)
              add_mult_objs_to_inven(crtr, item_ptr, items);
           end
           if i_count<=6 then begin
              reserv_item_tnk_ptr_6:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(dude_obj, reserv_item_tnk_ptr_6);
           end
           if i_count<=5 then begin
              reserv_item_tnk_ptr_5:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(dude_obj, reserv_item_tnk_ptr_5);
           end
           if i_count<=4 then begin
              reserv_item_tnk_ptr_4:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(dude_obj, reserv_item_tnk_ptr_4);
           end
           //
           if i_count<=3 then begin
              reserv_item_ptr_1:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(dude_obj, reserv_item_ptr_1);
           end
           if i_count<=2 then begin
              reserv_item_ptr_2:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(dude_obj, reserv_item_ptr_2);
           end
           if i_count<=1 then begin
              reserv_item_ptr_3:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(dude_obj, reserv_item_ptr_3);
           end
           if i_count==0 then begin
              reserv_item_ptr_4:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(dude_obj, reserv_item_ptr_4);
           end
        end else begin
           move_obj_inven_to_obj(obj_ptr, crtr);
           if i_count<=6 then begin
              reserv_item_tnk_ptr_3:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(obj_ptr, reserv_item_tnk_ptr_3);
           end
           if i_count<=5 then begin
              reserv_item_tnk_ptr_2:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(obj_ptr, reserv_item_tnk_ptr_2);
           end
           if i_count<=4 then begin
              reserv_item_tnk_ptr_1:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(obj_ptr, reserv_item_tnk_ptr_1);
           end
           //
           if i_count<=3 then begin
              reserv_item_trd_ptr_1:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(obj_ptr, reserv_item_trd_ptr_1);
           end
           if i_count<=2 then begin
              reserv_item_trd_ptr_2:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(obj_ptr, reserv_item_trd_ptr_2);
           end
           if i_count<=1 then begin
              reserv_item_trd_ptr_3:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(obj_ptr, reserv_item_trd_ptr_3);
           end
           if i_count==0 then begin
              reserv_item_trd_ptr_4:=create_object_sid(RESERVED_ITEM00, 1, 2, 2);
              add_obj_to_inven(obj_ptr, reserv_item_trd_ptr_4);
           end
        end

        move_obj_inven_to_obj(crtr, obj_ptr);
        destroy_object(crtr);

end

procedure Remove_ReservItem begin

          if reserv_item_trd_ptr_1 != 0 or reserv_item_tnk_ptr_3 != 0 then begin
             if reserv_item_trd_ptr_1 != 0 then destroy_object(reserv_item_trd_ptr_1);
             if reserv_item_trd_ptr_2 != 0 then destroy_object(reserv_item_trd_ptr_2);
             if reserv_item_trd_ptr_3 != 0 then destroy_object(reserv_item_trd_ptr_3);
             if reserv_item_trd_ptr_4 != 0 then destroy_object(reserv_item_trd_ptr_4);
             reserv_item_trd_ptr_1:=0;
             reserv_item_trd_ptr_2:=0;
             reserv_item_trd_ptr_3:=0;
             reserv_item_trd_ptr_4:=0;
             // for trunk
             if reserv_item_tnk_ptr_1 != 0 then destroy_object(reserv_item_tnk_ptr_1);
             if reserv_item_tnk_ptr_2 != 0 then destroy_object(reserv_item_tnk_ptr_2);
             if reserv_item_tnk_ptr_3 != 0 then destroy_object(reserv_item_tnk_ptr_3);
             reserv_item_tnk_ptr_1:=0;
             reserv_item_tnk_ptr_2:=0;
             reserv_item_tnk_ptr_3:=0;
          end
          // PC
          if reserv_item_ptr_1 != 0 or reserv_item_tnk_ptr_6 != 0 then begin
            if reserv_item_ptr_1 != 0 then destroy_object(reserv_item_ptr_1);
            if reserv_item_ptr_2 != 0 then destroy_object(reserv_item_ptr_2);
            if reserv_item_ptr_3 != 0 then destroy_object(reserv_item_ptr_3);
            if reserv_item_ptr_4 != 0 then destroy_object(reserv_item_ptr_4);
            reserv_item_ptr_1:=0;
            reserv_item_ptr_2:=0;
            reserv_item_ptr_3:=0;
            reserv_item_ptr_4:=0;
            // for trunk
            if reserv_item_tnk_ptr_4 != 0 then destroy_object(reserv_item_tnk_ptr_4);
            if reserv_item_tnk_ptr_5 != 0 then destroy_object(reserv_item_tnk_ptr_5);
            if reserv_item_tnk_ptr_6 != 0 then destroy_object(reserv_item_tnk_ptr_6);
            reserv_item_tnk_ptr_4:=0;
            reserv_item_tnk_ptr_5:=0;
            reserv_item_tnk_ptr_6:=0;
         end
         
end

procedure IsBarter begin

    variable Crtr_a1:=get_sfall_arg;
    trader_ptr:=get_sfall_arg;
    //Crtr_a0:=get_sfall_arg;
    //Crtr_a3:=get_sfall_arg;

    //display_msg("c1 in barter:"+ Crtr_a1);
    //display_msg("c2 in barter:"+ Crtr_a2);
    //display_msg("c3 in barter:"+ Crtr_a3);
    //display_msg("name:"+ obj_name(Crtr_a1));
    //display_msg("name:"+ item_caps_total(Crtr_a2));

    dude_in_barter:=true;
    dude_enter_barter:=true;
    
end

procedure hUseSkills begin

   variable Crtr:=get_sfall_arg;
   variable Obj:=get_sfall_arg;
   if get_sfall_arg!=SKILL_STEAL then uSkill:=true;

end

procedure check_bags_items(variable ptr) begin
variable itempack, itembag, itembag2, total:=0;

    itembag:=obj_carrying_pid_obj(ptr, PID_BAG);
    if obj_item_subtype(itembag)==item_type_container then total+=obj_is_carrying_obj_pid(ptr, PID_BAG);
    itembag2:=obj_carrying_pid_obj(ptr, PID_BROWN_BAG);
    if obj_item_subtype(itembag2)==item_type_container then total+=obj_is_carrying_obj_pid(ptr, PID_BROWN_BAG);
    itempack:=obj_carrying_pid_obj(ptr, PID_BACKPACK);
    if obj_item_subtype(itempack)==item_type_container then total+=obj_is_carrying_obj_pid(ptr, PID_BACKPACK);

    return total;
    
end

#define CheckObj(who)  if who##_In_Party and is_in_array(who##_Ptr, object_array) then return who##_Ptr;

procedure Check_Party_Obj begin

   CheckObj(Vic) else
   CheckObj(Myron) else
   CheckObj(Marcus) else
   CheckObj(MacRae) else
   CheckObj(Sulik) else
   CheckObj(Lenny) else
   CheckObj(Cyberdog) else
   CheckObj(Goris) else
   CheckObj(Davin) else
   CheckObj(Miria) else
   CheckObj(Robobrain) else
   CheckObj(Dogmeat) else
   CheckObj(K9) else
   CheckObj(Doc) else return -1;
   
end

procedure NodeNone begin
end
