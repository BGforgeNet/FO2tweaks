// gl_g_party_no_burst.ssl
/* Include Files */
#include ".\headers\define.h"
#include ".\headers\command.h"
#include ".\_sfall383_headers\define_extra.h"
#include ".\_sfall383_headers\sfall.h"
#include ".\_sfall383_headers\dik.h"
#include ".\!pbs_rebalance_sources\_pbs_main\lib.misc.h"


procedure start;
procedure keypress_hook_handler;
procedure toggle_burst(variable mode, variable manual);
procedure set_burst(variable manual);


procedure toggle_burst(variable mode, variable manual) begin
  variable new_mode;
  if (party_size > 1) then begin
    variable who;
    foreach who in party_member_list(0) begin
      if (who != dude_obj) then begin
        set_critter_burst_disable(who, mode);
      end
    end
    if manual == 1 then begin
      if (mode == 0) then begin
        new_mode := 1;
      end else begin
        new_mode := 0;
      end
    end else begin
      new_mode := mode;
    end
    set_sfall_global("g_burst0", new_mode);
  end
end

procedure set_burst(variable manual) begin
  variable burst_enabled;
  variable burst_msg;
  variable noburst_msg;
  burst_msg := get_ini_string("fo2tweaks.ini|" + "SETTINGS|" + "BurstMessage");
  noburst_msg := get_ini_string("fo2tweaks.ini|" + "SETTINGS|" + "NoBurstMessage");
  burst_enabled := get_sfall_global_int("g_burst0");
  if (manual == 1) then begin
    if (burst_enabled == 0) then begin
      float_msg(dude_obj, burst_msg, FLOAT_MSG_BLUE);
    end else begin
      float_msg(dude_obj, noburst_msg, FLOAT_MSG_BLUE);
    end
  end
  debug_msg("g_burst0: " + burst_enabled);
  call toggle_burst(burst_enabled, manual);
end

procedure keypress_hook_handler begin
  variable burst_key;
  variable burst_enabled;
  burst_key := parse_hotkey(get_ini_string("fo2tweaks.ini|" + "SETTINGS|" + "BurstKey"));
  if (not(in_world_map)) then begin
    if (key_pressed(burst_key)) then begin
      call set_burst(1); //manual
    end
  end
end

procedure start begin
  variable burst_enabled;
  burst_enabled := get_sfall_global_int("g_burst0");
  if (game_loaded) then begin
    set_global_script_repeat(300);
    register_hook_proc(HOOK_KEYPRESS, keypress_hook_handler);
  end else begin
    call set_burst(0); //not manual
  end
end
