LOAD_TRA ~%tra_dir%/%LANGUAGE%/strings.tra~

//Vault city comp: circumvent node023, conceal vault 15
LAF g_dat_extract STR_VAR file_path = ~scripts\vicencom.int~ END
LAF g_replace_int_text_nocase
  STR_VAR
    script_name = ~vicencom~
    old_text =
~
procedure Node023
begin
	if (metarule(17, 9) == 0) then begin
		debug_msg("  mark_on_map(" + 9 + ")");
		mark_area_known(0, 9, 1);
	end
	if (local_var(3) == 0) then begin
		set_local_var(3, local_var(3) + 1);
		gsay_reply(838, message_str(838, 234) + " " + message_str(838, 235));
	end
	else begin
		gsay_reply(838, 234);
	end
	giq_option(4, 838, 236, Node033a, 50);
	giq_option(4, 838, 237, Node033b, 50);
end
~
    new_text =
~
procedure Node023
begin
	gsay_reply(838, 234);
	giq_option(4, 838, 232, Node021, 50);
	giq_option(4, 838, 233, Node006a, 50);
end
~
END

//Vault city comp: conceal vault 15 in "insert pipboy" dialogue option
LAF g_replace_int_text_nocase 
  STR_VAR
    script_name = ~vicencom~
    old_text =
~
	if (metarule(17, 9) == 0) then begin
		debug_msg("  mark_on_map(" + 9 + ")");
		mark_area_known(0, 9, 1);
	end
~
    new_text = ~~
END

//Vault city comp: replace text to make clear there's no coordinates
COPY ~data/text/english/dialog/vicencom.msg~ ~data/text/english/dialog/vicencom.msg~
  SPRINT t1 @101
  SPRINT t2 @102
  REPLACE_TEXTUALLY EXACT_MATCH ~%t1%~ ~%t2%~
BUT_ONLY


//Tandi: reveal Vault 15 location
LAF g_dat_extract STR_VAR file_path = ~scripts\shtandi.int~ END
LAF g_replace_int_text_nocase 
  STR_VAR
    script_name = ~shtandi~
    old_text = ~gsay_reply(172, 116);~
    new_text = ~gsay_reply(172, 116);
                mark_area_known(0, 9, 1);
               ~
END


//Redding: Lou doesn't talk aobut San Francisco
LAF g_dat_extract STR_VAR file_path = ~scripts\rclou.int~ END
LAF g_replace_int_text_nocase //conceal "where's san fran" option
  STR_VAR
    script_name = ~rclou~
    old_text = ~giq_option(6, 682, 115, Node011, 50);~
    new_text = ~~
END
COPY ~data/text/english/dialog/rclou.msg~ ~data/text/english/dialog/rclou.msg~
  SPRINT t1 @103
  SPRINT t2 @104
  REPLACE_TEXTUALLY EXACT_MATCH ~%t1%~ ~%t2%~
BUT_ONLY


//Vault 15 comp: don't reveal Vault 13
LAF g_dat_extract STR_VAR file_path = ~scripts\bscomp1.int~ END
LAF g_replace_int_text_nocase
  STR_VAR
    script_name = ~bscomp1~
    old_text = ~mark_area_known(0, 11, 1);~
    new_text = ~~
END
LAF g_replace_int_text_nocase
  STR_VAR
    script_name = ~bscomp1~
    old_text = ~debug_msg("  mark_on_map(" + 11 + ")");~
    new_text = ~~
END
OUTER_SPRINT f ~text\english\dialog\bscomp1.msg~
LAF g_dat_extract STR_VAR file_path = ~%f%~ END
COPY ~data/%f%~ ~data/%f%~
  SPRINT t1 @105
  SPRINT t2 @106
  REPLACE_TEXTUALLY EXACT_MATCH ~%t1%~ ~%t2%~
  SPRINT t1 @107
  SPRINT t2 @108
  REPLACE_TEXTUALLY EXACT_MATCH ~%t1%~ ~%t2%~
BUT_ONLY


//Hakunin: don't reveal Navarro
LAF g_dat_extract STR_VAR file_path = ~scripts\ahhakun.int~ END
LAF g_replace_int_text_nocase 
  STR_VAR
    script_name = ~ahhakun~
    old_text =
~
			if (metarule(17, 15) == 0) then begin
				debug_msg("  mark_on_map(" + 15 + ")");
				mark_area_known(0, 15, 1);
			end
~
    new_text = ~~
END
