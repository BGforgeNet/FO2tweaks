#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\sfall\define_extra.h"
#include "..\headers\sfall\sfall.h"
#include "..\headers\sfall\dik.h"
#include "..\headers\sfall\lib.misc.h"
#include "..\headers\sfall\lib.arrays.h"
#include "..\headers\fo2tweaks\fo2tweaks.h"

#define NAME "gl_g_healing_revision"

#define all_disabilities \
  (DAM_CRIP_ARM_LEFT bwor \
  DAM_CRIP_ARM_RIGHT bwor \
  DAM_CRIP_LEG_LEFT bwor \
  DAM_CRIP_LEG_RIGHT bwor \
  DAM_BLIND)

#define is_disabled(x) (critter_state(x) bwand all_disabilities)

#define doctor_used_array "g_doctor_used"
#define fa_used_array "g_fa_used"

#define doctor_skill_name 107
#define fa_skill_name 106

#define sec_healing_revision "healing_revision"
#define set_healing_not_in_combat_msg "not_in_combat_msg"
#define set_healing_already_used_msg "already_used_msg"
#define set_healing_use_items "use_items"
#define set_healing_heal_disabilities_msg "heal_disabilites_msg"
#define set_healing_fail_disabilities_msg "fail_disabilites_msg"
#define set_healing_fail_msg "fail_msg"
#define set_healing_no_chance_msg "no_chance_msg"
#define set_healing_healthy_msg "healthy_msg"
#define set_healing_doctor_items "doctor_items"
#define set_healing_fa_items "first_aid_items"
#define set_healing_living_anatomy_min_doctor "living_anatomy_min_doctor"
#define set_healing_living_anatomy_msg "free_living_anatomy_msg"
#define set_healing_living_anatomy_msg2 "free_living_anatomy_msg2"

procedure start;
procedure healing_handler;
procedure missing_hp(variable who);
procedure grant_xp(variable hp, variable skill);

variable begin
  doctor_used;
  fa_used;
  enabled;
  not_in_combat_msg;
  already_used_msg;
  no_chance_msg;
  heal_disabilities_msg;
  fail_disabilities_msg;
  healthy_msg;
  fail_msg;
  use_items := 0;
  doctor_min := 20;
  doctor_mult := 10;
  fa_mult := 5;
  heal_max := 15;
  cur_month;
  last_month;
  cur_day;
  last_day;
  doctor_items;
  fa_items;
  living_anatomy_min_doctor;
  living_anatomy_msg;
  living_anatomy_msg2;
end

procedure heal_disabilities(variable user, variable target, variable effective_skill) begin
  variable chance := effective_skill - doctor_min; //percentage
  ndebug("user " + user + ", target " + target + ", eff. doctor " + effective_skill);
  ndebug("chance " + chance + " eff. skill " + effective_skill + " min. doctor " + doctor_min);
  if chance < 1 then begin
    display_msg(no_chance_msg);
  end else begin
    variable r := random(1, 100);
    if r < chance then begin
      display_msg(heal_disabilities_msg);
      critter_uninjure(target, all_disabilities);
    end else begin
      display_msg(fail_disabilities_msg);
    end
  end
end

procedure is_healthy(variable target) begin
  variable max_hp := get_critter_stat(target, STAT_max_hp);
  variable cur_hp := get_critter_stat(target, STAT_current_hp);
  if cur_hp >= max_hp and not is_disabled(target) then begin
    return true;
  end else begin
    return false;
  end
end

procedure get_healed_hp(variable target, variable doctor_level, variable fa_level) begin
  variable mhp := missing_hp(target);
  variable healed := 0;
  variable healed_str;
  if doctor_level > 100 then doctor_level := 100;
  if fa_level > 100 then fa_level := 100;
  //healed := (fa_level/100.0)*fa_mult + (doctor_level/100.0)*(fa_level/100.0)*(heal_max - doctor_mult);
  healed := (doctor_level/100.0)*(fa_level/100.0)*doctor_mult + random(-1, 1);
  if healed < 0 then healed := 0;
  healed_str := sprintf("%f", healed);
  ndebug("missing " + mhp + " doctor_level " + doctor_level + " fa_level " + fa_level + " healed " + healed_str
    + " max: " + heal_max +" |"+ fa_mult +"|" + doctor_mult);
  healed := floor2(healed);
  if healed > mhp then healed := mhp;
  return healed;
end

procedure get_fa_healed(variable target, variable fa_level) begin
  variable mhp := missing_hp(target);
  variable healed := 0;
  variable healed_str;
  if fa_level > 100 then fa_level := 100;
  //healed := (fa_level/100.0)*fa_mult + (doctor_level/100.0)*(fa_level/100.0)*(heal_max - doctor_mult);
  healed := fa_mult * fa_level / 100.0 + random(-1, 1);
  if healed < 0 then healed := 0;
  healed_str := sprintf("%f", healed);
  ndebug("missing " + mhp + " fa_level " + fa_level + " healed " + healed_str + " | "+ fa_mult);
  healed := floor2(healed);
  if healed > mhp then healed := mhp;
  return healed;
end


procedure missing_hp(variable target) begin
  variable max_hp := get_critter_stat(target, STAT_max_hp);
  variable cur_hp := get_critter_stat(target, STAT_current_hp);
  variable mhp := max_hp - cur_hp;
  return mhp;
end

procedure use_doctor(variable user, variable target, variable skill_bonus, variable interactive := true) begin
  variable doctor_level := get_party_skill_level(SKILL_DOCTOR);
  variable fa_level := get_party_skill_level(SKILL_FIRST_AID);
  variable effective_doctor := doctor_level + skill_bonus;
  variable test1;
  variable i;
  variable tmp;
  ndebug("doctor level " + doctor_level + ", skill bonus " + skill_bonus + ", effective " + effective_doctor);

  if interactive then begin
    if is_healthy(target) then begin // healthy, nothing to do
      display_msg(healthy_msg);
      return false;
    end else begin
      ndebug("Check doctor_used");
      foreach i in doctor_used begin
        ndebug(i);
      end
      if scan_array(doctor_used, obj_pid(target)) != -1 then begin //already used doctor today
        display_msg(already_used_msg);
        return false;
      end
    end
  end

  //if dude or companion using skill not in manual mode, search for a helper item in all of party inventory
  if use_items == 1 and skill_bonus == 0 and scan_array(party_member_list_critters, user) != -1 then begin
    ndebug("todo: find best item, boost skill, spend item charges");
    //effective skill := skill_level + item_bonus;
  end

  ndebug("user " + user + ", target " + target + ", eff. doctor " + effective_doctor);
  ndebug("critter state " + sprintf("%d", critter_state(target)));

  if is_disabled(target) then call heal_disabilities(user, target, effective_doctor);

  if missing_hp(target) > 0 then begin
    variable name := obj_name(target);
    variable hp := get_healed_hp(target, effective_doctor, fa_level);
    ndebug("trying to heal " + name);

    call array_push(doctor_used, obj_pid(target));
    save_array(doctor_used_array, doctor_used);
    set_sfall_global("g_heal_d", get_day);

//    gfade_in(1);
    game_time_advance(game_ticks(3600));
    if hp < 1 then begin
      display_msg(fail_msg);
      return false;
    end
    critter_heal(target, hp);
//    gfade_out(1);
    display_msg(obj_name(target) + " healed for " + hp + " HP");
    call grant_xp(hp, SKILL_DOCTOR);
  end
end

procedure use_first_aid(variable user, variable target, variable skill_bonus, variable interactive := true) begin
  variable fa_level := get_party_skill_level(SKILL_FIRST_AID);
  variable effective_fa := fa_level + skill_bonus;
  variable i;
  ndebug("fa level " + fa_level + ", skill bonus " + skill_bonus + ", effective " + effective_fa);
  ndebug("userfa " + user + " target " + target);

  if interactive then begin
    if is_healthy(target) then begin // healthy, nothing to do
      display_msg(healthy_msg);
      return false;
    end else begin
      if scan_array(fa_used, obj_pid(target)) != -1 then begin //already used fa today
        display_msg(already_used_msg);
        return false;
      end
    end
  end

  if missing_hp(target) > 0 then begin
    variable name := obj_name(target);
    variable hp := get_fa_healed(target, fa_level);
    ndebug("trying to heal " + name);

    call array_push(fa_used, obj_pid(target));
    save_array(fa_used_array, doctor_used);
    set_sfall_global("g_heal_d", get_day);

//    gfade_in(1);
    game_time_advance(game_ticks(1800));
    if hp < 1 then begin
      display_msg(fail_msg);
      return false;
    end
    critter_heal(target, hp);
//    gfade_out(1);
    display_msg(obj_name(target) + " healed for " + hp + " HP");
    call grant_xp(hp, SKILL_FIRST_AID);
  end
end

procedure get_best_helper_item(variable skill) begin
  variable who, item, items;
  if skill == SKILL_DOCTOR then items := doctor_items;
  if skill == SKILL_FIRST_AID then items := fa_items;
  foreach item in items begin
    foreach who in party_member_list_critters begin
      if obj_is_carrying_obj_pid(who, item) then begin
        ndebug(obj_name(who) + " is carrying " + obj_name(item) + ", using it for " + mstr_skill(skill));
        return([who, item]);
      end
    end
  end
end

procedure reset_heal_arrays begin
  ndebug("resetting heal arrays");
//  free_array(fa_used);
  fa_used := create_array(0,0);
  save_array(fa_used_array, fa_used);
//  free_array(doctor_used);
  doctor_used := create_array(0, 0);
  save_array(doctor_used_array, doctor_used);
end

procedure update_heal_arrays begin
  variable last_day := get_sfall_global_int("g_heal_d");
  ndebug("Last day " + last_day );
  if last_day == 0 then return;
  if get_day != last_day then call reset_heal_arrays;
end

procedure grant_xp(variable hp, variable skill) begin
  variable xp := hp * 10;
  variable skill_name;
  if skill == SKILL_DOCTOR then begin
    skill_name := mstr_skill(doctor_skill_name);
  end else begin
    skill_name := mstr_skill(fa_skill_name);
  end
  give_exp_points(xp);
  display_msg("You receive " + xp + " experience for successful use of " + skill_name + " skill." );
end

procedure use_skill(variable user, variable target, variable skill, variable skill_bonus) begin
  call update_heal_arrays;
  ndebug("user1 " + user + " target " + target);
  if skill == SKILL_DOCTOR then begin
    call use_doctor(user, target, skill_bonus);
  end
  ndebug("user2 " + user + " target " + target);
  if skill == SKILL_FIRST_AID then begin
    call use_first_aid(user, target, skill_bonus);
  end
  ndebug("user3 " + user + " target " + target);
//  if skill != SKILL_DOCTOR and skill != SKILL_FIRST_AID then set_sfall_return(-1); //failsafe
end

procedure healing_handler begin
  variable user := get_sfall_arg;
  variable target := get_sfall_arg;
  variable skill := get_sfall_arg;
  variable skill_bonus := get_sfall_arg;
  ndebug("user " + user + " target " + target);
  if enabled == 1 and (skill == SKILL_FIRST_AID or skill == SKILL_DOCTOR) then begin
    set_sfall_return(0); //override
    if combat_is_initialized then begin
      display_msg(not_in_combat_msg);
    end else begin
      call use_skill(user, target, skill, skill_bonus);
    end
    set_sfall_return(0); //override again?
  end else begin
    set_sfall_return(-1); //default engine handler
  end
end

procedure rest_handler begin
  variable who;
  ndebug("rest ping");
  call update_heal_arrays;
  foreach who in party_member_list_critters begin
    if not is_healthy(who) and scan_array(doctor_used, obj_pid(who)) == -1 then begin
      call use_doctor(dude_obj, who, 0, false); // best item will be found automatically
    end
    if not is_healthy(who) and scan_array(fa_used, obj_pid(who)) == -1 then begin
      call use_first_aid(dude_obj, who, 0, false);
    end
  end
  intface_redraw;
end

procedure travel_handler begin
  variable who;
  ndebug("travel ping");
  foreach who in party_member_list_critters begin
    if not is_healthy(who) and scan_array(doctor_used, obj_pid(who)) == -1 then begin
      call use_doctor(dude_obj, who, 0, false); // best item will be found automatically
    end
    if not is_healthy(who) and scan_array(fa_used, obj_pid(who)) == -1 then begin
      call use_first_aid(dude_obj, who, 0, false);
    end
  end
end

procedure map_enter_p_proc begin
  variable who;
  if living_anatomy_min_doctor < 300 then begin
    foreach who in list_as_array(LIST_CRITTERS) begin
      ndebug(obj_name(who) + " has Doctor of " + critter_skill_level(who, SKILL_DOCTOR));
      if critter_skill_level(who, SKILL_DOCTOR) >= living_anatomy_min_doctor
        and not has_trait(TRAIT_PERK, who, PERK_living_anatomy_perk)
      then begin
        ndebug(obj_name(who) + " gets living anatomy!");
        critter_add_trait(who, TRAIT_PERK, PERK_living_anatomy_perk, 1);
        if in_party(who) then begin
          display_msg(obj_name(who) + " " + living_anatomy_msg2);
          float_msg(who, living_anatomy_msg, FLOAT_MSG_WHITE);
        end
      end
    end
  end
end

procedure start begin
  if game_loaded then begin
    enabled := fo2tweaks_setting(sec_main, set_healing_revision);
    if enabled == 1 then begin
      use_items := fo2tweaks_setting(sec_healing_revision, set_healing_use_items);

      not_in_combat_msg := fo2tweaks_string(sec_healing_revision, set_healing_not_in_combat_msg);
      already_used_msg := fo2tweaks_string(sec_healing_revision, set_healing_already_used_msg);
      no_chance_msg := fo2tweaks_string(sec_healing_revision, set_healing_no_chance_msg);
      healthy_msg := fo2tweaks_string(sec_healing_revision, set_healing_healthy_msg);
      fail_msg := fo2tweaks_string(sec_healing_revision, set_healing_fail_msg);
      heal_disabilities_msg := fo2tweaks_string(sec_healing_revision, set_healing_heal_disabilities_msg);
      fail_disabilities_msg := fo2tweaks_string(sec_healing_revision, set_healing_fail_disabilities_msg);

      living_anatomy_min_doctor := fo2tweaks_setting(sec_healing_revision, set_healing_living_anatomy_min_doctor);
      living_anatomy_msg := fo2tweaks_string(sec_healing_revision, set_healing_living_anatomy_msg);
      living_anatomy_msg2 := fo2tweaks_string(sec_healing_revision, set_healing_living_anatomy_msg2);

      doctor_items := fo2tweaks_comsep_setting(sec_healing_revision, set_healing_doctor_items);
      fa_items := fo2tweaks_comsep_setting(sec_healing_revision, set_healing_fa_items);

      doctor_used := load_array(doctor_used_array);
      if doctor_used == 0 then doctor_used := create_array(0,0);
      fa_used := load_array(fa_used_array);
      if fa_used == 0 then fa_used := create_array(0,0);

      register_hook_proc(HOOK_USESKILL, healing_handler);
      register_hook_proc(HOOK_RESTTIMER, rest_handler);

      //reset healed arrays at midnight, any screen
      set_global_script_type(3);
      set_global_script_repeat(1800);

      ndebug("initialized");
    end
  end else begin
    if enabled == 1 then begin
      call update_heal_arrays;
      if in_world_map then begin
        call travel_handler;
      end
    end
  end
end
