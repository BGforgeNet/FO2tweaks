#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\sfall\define_extra.h"
#include "..\headers\sfall\sfall.h"
#include "..\headers\sfall\dik.h"
#include "..\headers\sfall\lib.misc.h"
#include "..\headers\fo2tweaks\fo2tweaks.h"

#define in_combat (get_game_mode bwand COMBAT)

variable burst_msg;
variable noburst_msg;
variable burst_key;
variable burst_mode;

procedure start;
procedure keypress_hook_handler;
procedure gamemode_hook_handler;
procedure toggle_burst;
procedure set_burst;

procedure set_burst begin
  variable who;
  variable burst_disable;
  burst_mode := get_sfall_global_int("g_burstv");

  if (true_party_size > 0) then begin
    debug_msg("gl_g_party_burst_control: setting burst_mode to " + burst_mode);

    if burst_mode == 1 then begin
      burst_disable := 0;
    end else begin
      burst_disable := 1;
    end

    foreach who in party_member_list_critters begin
      if (who != dude_obj) then begin
        set_critter_burst_disable(who, burst_disable);
      end
    end

  end
end

procedure toggle_burst begin
  variable msg;
  burst_mode := get_sfall_global_int("g_burstv");
  debug_msg("gl_g_party_burst_control: old g_burstv is " + burst_mode);
  if burst_mode == 0 then begin
    msg := burst_msg;
    burst_mode := 1;
  end else begin
    msg := noburst_msg;
    burst_mode := 0;
  end
  float_msg(dude_obj, msg, FLOAT_MSG_BLUE);
  set_sfall_global("g_burstv", burst_mode);
  call set_burst;
  debug_msg("gl_g_party_burst_control: new g_burstv is " + burst_mode);
end

procedure keypress_hook_handler begin
  if (key_pressed(burst_key) and not(in_world_map)) then begin
    call toggle_burst;
  end
end

procedure gamemode_hook_handler begin
  if in_combat then begin
    call set_burst;
  end
end

procedure start begin
  if game_loaded then begin
    variable enabled := get_ini_setting(fo2tweaks_ini + "|" + sec_main + "|" + set_burst_control);
    if enabled then begin
      burst_msg := get_ini_string(fo2tweaks_ini + "|" + sec_burst_control + "|" + set_burst_message_on);
      noburst_msg := get_ini_string(fo2tweaks_ini + "|" + sec_burst_control + "|" + set_burst_message_off);
      burst_key := parse_hotkey(get_ini_string(fo2tweaks_ini + "|" + sec_burst_control + "|" + set_burst_key));
      register_hook_proc(HOOK_KEYPRESS, keypress_hook_handler);
      register_hook_proc(HOOK_GAMEMODECHANGE, gamemode_hook_handler);
      burst_mode := get_sfall_global_int("g_burstv");
      debug_msg("gl_g_party_burst_control initialized, current burst mode: " + burst_mode);
    end
  end
end
