#include "..\headers\define.h"
#include "..\headers\command.h"
#include "..\headers\sfall\define_extra.h"
#include "..\headers\sfall\sfall.h"
#include "..\headers\sfall\dik.h"
#include "..\headers\sfall\lib.misc.h"
#include "..\headers\fo2tweaks\fo2tweaks.h"

#define in_combat (get_game_mode bwand COMBAT)
#define NAME "gl_g_party_burst_control"

variable msg_burst_float;
variable msg_noburst_float;
variable msg_burst_display;
variable msg_noburst_display;
variable burst_key;
variable burst_mode;

procedure start;
procedure keypress_hook_handler;
procedure gamemode_hook_handler;
procedure toggle_burst;
procedure set_burst;

procedure set_burst begin
  variable who;
  variable burst_disable;
  burst_mode := get_sfall_global_int("g_burstv");

  if (true_party_size > 0) then begin
    ndebug("setting burst_mode to " + burst_mode);

    if burst_mode == 1 then begin
      burst_disable := 0;
    end else begin
      burst_disable := 1;
    end

    foreach who in party_member_list_critters begin
      if (who != dude_obj) then begin
        set_critter_burst_disable(who, burst_disable);
      end
    end

  end
end

procedure toggle_burst begin
  variable msg_float, msg_display;
  burst_mode := get_sfall_global_int("g_burstv");
  ndebug("old g_burstv is " + burst_mode);
  if burst_mode == 0 then begin
    msg_float := msg_burst_float;
    msg_display := msg_burst_display;
    burst_mode := 1;
  end else begin
    msg_float := msg_noburst_float;
    msg_display := msg_noburst_display;
    burst_mode := 0;
  end
  float_msg(dude_obj, msg_float, FLOAT_MSG_BLUE);
  display_msg(msg_display);
  set_sfall_global("g_burstv", burst_mode);
  call set_burst;
  ndebug("new g_burstv is " + burst_mode);
end

procedure keypress_hook_handler begin
  if key_pressed(burst_key) and not in_world_map then begin
    call toggle_burst;
  end
end

procedure gamemode_hook_handler begin
  if in_combat then begin
    call set_burst;
  end
end

procedure start begin
  if game_loaded then begin
    variable enabled := fo2tweaks_setting(sec_main, set_burst_control);
    if enabled == 1 then begin
      msg_burst_float := fo2tweaks_string(sec_burst_control, set_burst_float_on);
      msg_noburst_float := fo2tweaks_string(sec_burst_control, set_burst_float_off);
      msg_burst_display := fo2tweaks_string(sec_burst_control, set_burst_display_on);
      msg_noburst_display := fo2tweaks_string(sec_burst_control, set_burst_display_off);
      burst_key := parse_hotkey(fo2tweaks_string(sec_burst_control, set_burst_key));
      register_hook_proc(HOOK_KEYPRESS, keypress_hook_handler);
      register_hook_proc(HOOK_GAMEMODECHANGE, gamemode_hook_handler);
      burst_mode := get_sfall_global_int("g_burstv");
      ndebug("initialized, current burst mode: " + burst_mode);
    end
  end
end
