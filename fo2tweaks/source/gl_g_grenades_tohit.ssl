#include "..\headers\define.h"
#include "..\headers\sfall\sfall.h"
#include "..\headers\ecco\_pbs_main\mod.h"
#include "..\headers\fo2tweaks\fo2tweaks.h"

procedure start;
procedure tohit_hook;

procedure start begin
  if game_loaded then begin
    variable enabled := get_ini_setting(fo2tweaks_ini + "|" + sec_main + "|" + set_grenades_chance);
    if enabled == 1 then begin
      register_hook_proc(HOOK_TOHIT, tohit_hook);
      debug_msg("gl_g_grenades_tohit: initialized");
    end
  end
end

procedure tohit_hook begin
  variable tohit;
  variable attacker;
  variable target;
  variable explosive_weapon_pids;
  variable explosive_ammo_pids;
  variable pid;
  variable subtract_ac;

  explosive_weapon_pids := load_comsep_ini_setting(fo2tweaks_ini, sec_grenades_chance, set_grenades_weapons);
  explosive_ammo_pids := load_comsep_ini_setting(fo2tweaks_ini, sec_grenades_chance, set_grenades_ammo);

  tohit := get_sfall_arg;
  attacker := get_sfall_arg;
  target := get_sfall_arg;

  //check weapon
  foreach pid in explosive_weapon_pids begin
    if get_active_weapon_pid(attacker) == pid then begin
      subtract_ac := true;
      break;
    end
  end
  //check ammo
  if subtract_ac == false then begin
    foreach pid in explosive_ammo_pids begin
      if get_active_ammo_pid(attacker) == pid then begin
        subtract_ac := true;
        break;
      end
    end
  end

  //add target's armor ac to hit chance
  if subtract_ac == true then begin
    variable nat_ac, armor, armor_ac;
//    nat_ac := get_critter_stat(target, STAT_ac);
    armor := critter_inven_obj(target, INVEN_TYPE_WORN);
    if obj_item_subtype(armor) == item_type_armor then begin
      armor_ac := get_proto_data(obj_pid(armor), PROTO_AR_AC);
      tohit := tohit + armor_ac;
      debug_msg("gl_g_grenades_tohit: chance to hit increased by " + armor_ac);
    end
  end

  //cap chance
  if (tohit > 95) then begin
    tohit := 95;
  end

  set_sfall_return(tohit);
  set_sfall_arg(0, tohit);

end
