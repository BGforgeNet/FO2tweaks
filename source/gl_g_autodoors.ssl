#define SCRIPT_REALNAME "gl_g_autodoors"

#include "headers/fo2tweaks/fo2tweaks.h"

procedure start;
procedure map_enter_p_proc;
procedure set_door_flag(variable state);
procedure gamemode_handler;

#define PORTAL      (0)
#define DOOR_FLAGS  (0x24)
#define set_autodoors "autodoors"
variable only_once := 0;
variable old_mode := 0, new_mode;
variable enabled := 0;

procedure start begin
  if game_loaded then begin
    enabled := fo2tweaks_setting(sec_main, set_autodoors);
    if enabled == 1 then begin
      register_hook_proc(HOOK_GAMEMODECHANGE, gamemode_handler);
      call map_enter_p_proc;
      ndebug("initialized");
    end
  end
end

procedure map_enter_p_proc begin
  if enabled == 1 then call set_door_flag(FLAG_WALKTHRU);
end

procedure set_door_flag(variable flag) begin
  variable obj, pid;
  foreach (obj in list_as_array(LIST_SCENERY)) begin
    if is_door(obj) then set_proto_data(obj_pid(obj), DOOR_FLAGS, flag);
  end
end

procedure gamemode_handler begin
  new_mode := get_game_mode;
  if mode_ended(COMBAT, old_mode, new_mode) then begin
    call set_door_flag(FLAG_WALKTHRU);
    ndebug("combat ended, enabled walking through doors");
  end
  if mode_started(COMBAT, old_mode, new_mode) then begin
    call set_door_flag(0);
    ndebug("combat started, disabled walking through doors");
  end
  old_mode := new_mode;
end
