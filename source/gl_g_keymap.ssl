#define SCRIPT_REALNAME "gl_g_keymap"

#include "headers/fo2tweaks/fo2tweaks.h"
#define set_keymap "keymap"
#define sec_keymap "keymap"

variable enabled;
variable keymap;

procedure start;

procedure keypress_handler begin
  variable pressed = get_sfall_arg, key = get_sfall_arg;
  variable k, v, new_key;

  new_key = scan_array(keymap, key);

  // Default save shortcuts: F4, F6. Any other shortcuts shouldn't be needed in savegame mode.
  if (get_game_mode bwand SAVEGAME) and not ( (new_key == DIK_F4) or (new_key == DIK_F6) ) then return;

  //remap
  if new_key != -1 then begin
    if pressed then ndebug("pressed key " + key + " changed to " + new_key);
    else ndebug("released key " + key + " changed to " + new_key);
    set_sfall_return(new_key);
    return;
  end

  //disable old key
  if keymap[key] != 0 then begin
    ndebug("old key " + key + " is not mapped, disabling");
    set_sfall_return(DIK_UNLABELED); //hope you don't use katakana my friend
  end
end

procedure start begin
  if game_loaded then begin
    enabled = fo2tweaks_setting(sec_main, set_keymap);
    if enabled == 1 then begin
      variable tmp = get_ini_section(fo2tweaks_ini, sec_keymap);
      variable k, v;
      keymap = temp_array_map;
      foreach k: v in tmp begin
        keymap[atoi(v)] = atoi(k); // reversed for array check
        ndebug("remapped key code " + v + " to " + k);
      end
      fix_array(keymap);
      register_hook_proc(HOOK_KEYPRESS, keypress_handler);
      ndebug("initialized");
    end
  end
end
