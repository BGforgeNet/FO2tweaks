#define SCRIPT_REALNAME "gl_g_gigolo"
#include "headers/fo2tweaks/fo2tweaks.h"

#define set_gigolo "gigolo_limit"
#define list_had_sex_with "g_sex_list" // global array to keep unique sex script numbers
variable sex_list; // var to keep list_had_sex_with
#define SGVAR_alt_sex_counter "g_gigolo" //global var to keep unique sex count. Different from array length, because some scripts are special (Kitty, Sally)
#define SGVAR_cats_paw_counter "g_gi_paw" // Cat's Paw counter
#define CATS_PAW_NUM_GIRLS 6
#define SGVAR_bathhouse1 "g_gi_ba1" // Bathouse, girl 1
#define SGVAR_bathhouse2 "g_gi_ba2" // Bathouse, girl 2 - Jenny
variable set_from_hook = 0; // avoid race condition in set_gvar hook
variable gigolo_limit;
variable map_unique; // stores list of objects with whim dude had sex on random maps (trappers, rave party). Reset on map enter
variable caps; // track dude caps for bathhouse procedure

procedure check_unique_map;
procedure cats_paw;
procedure check_unique(variable script, variable sex_list);
procedure bathhouse;

procedure set_gvar_hook begin
  variable gvar_index = get_sfall_arg;
  variable gvar_value = get_sfall_arg;
  variable current_script, alt_sex_counter;
  ndebug("setting gvar index: " + gvar_index + ", value: " + gvar_value);
  if gvar_index != GVAR_SEX_COUNTER
    and gvar_index != GVAR_GIGALO
  then return;

  if gvar_index == GVAR_GIGALO and set_from_hook == 0 then begin
    ndebug("game is trying to set gigolo, overriding");
    set_sfall_return(0);
    return;
  end

  current_script = get_script(dialog_obj); //sex really only happens in dialog
  ndebug("current script: " + current_script);

  if not check_unique(current_script, sex_list) then return;

  alt_sex_counter = get_sfall_global_int(SGVAR_alt_sex_counter);
  ndebug("new encounter: " + current_script);
  set_sfall_global(SGVAR_alt_sex_counter, alt_sex_counter + 1);
  if get_sfall_global_int(SGVAR_alt_sex_counter) >= gigolo_limit then begin
    set_from_hook = 1;
    set_global_var(GVAR_GIGALO, 1);
    ndebug("hooray, made gigolo!");
    set_from_hook = 0;
  end
end

procedure check_unique(variable script, variable sex_list) begin
  switch script begin
    case SCRIPT_NCKITTY: if cats_paw then return true;
    case SCRIPT_ECTRAPPR: if check_unique_map then return true;
    case SCRIPT_ECRAVPTY: if check_unique_map then return true;
    case SCRIPT_KCSALLY: if bathhouse then return true;
  end
  if is_in_array(script, sex_list) then begin
    ndebug("already counted, pass");
    return false;
  end
  return true;
end

// each critter is unique each time dude enters the map (rave party, etc)
procedure map_enter_p_proc begin
  map_unique = create_array(0,0);
end
procedure check_unique_map begin
  variable who = dialog_obj;
  ndebug("map unique check");
  if is_in_array(who, map_unique) then return false;
  ndebug("map unique check: passed");
  call array_push(who, map_unique);
  return true;
end

// Cat's Paw has 6 girls
procedure cats_paw begin
  variable counter = get_sfall_global_int(SGVAR_cats_paw_counter);
  ndebug("cat's paw check");
  if counter > CATS_PAW_NUM_GIRLS then return false;
  counter = counter + 1;
  set_sfall_global(SGVAR_cats_paw_counter, counter);
  ndebug("cat's paw check passed, new counter: " + counter);
  return true;
end

// Bathhouse: only 2 real girls
procedure bathhouse begin
  variable caps_change = caps - dude_caps;
  variable bath_gvar;
  if caps_change == 55 or caps_change == 70 then begin
    bath_gvar = get_sfall_global_int(SGVAR_bathhouse1);
    if bath_gvar == 0 then begin
      set_sfall_global(SGVAR_bathhouse1, 1);
      ndebug("bathhouse: first girl visited");
      return true;
    end
  end
  if caps_change == 125 or caps_change == 185 then begin
    bath_gvar = get_sfall_global_int(SGVAR_bathhouse2);
    if bath_gvar == 0 then begin
      set_sfall_global(SGVAR_bathhouse2, 1);
      ndebug("bathhouse: second girl visited");
      return true;
    end
  end
  return false;
end
procedure dialog_hook begin
  variable proc = get_sfall_arg;
  if proc != talk_proc then return;
  if get_script(dialog_obj) != SCRIPT_KCSALLY then return;
  caps = dude_caps;
  ndebug("talking to Sally, caps: " + caps);
end

procedure start begin
  if game_loaded then begin
    variable i;
    gigolo_limit = fo2tweaks_setting(sec_main, set_gigolo);
    if gigolo_limit > 0 then begin
      if global_var(GVAR_GIGALO) == 1 then begin
        ndebug("already gigolo, pass");
        return;
      end
      register_hook_proc(HOOK_SETGLOBALVAR, set_gvar_hook);
      register_hook_proc(HOOK_STDPROCEDURE, dialog_hook);
      sex_list = load_array(list_had_sex_with);
      if sex_list == 0 then begin
        sex_list = create_array(0,0);
        save_array(list_had_sex_with, sex_list);
      end
      ndebug("initialized");
    end
  end
end
