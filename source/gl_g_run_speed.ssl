#define SCRIPT_REALNAME "gl_g_run_speed"
#include "headers/fo2tweaks/fo2tweaks.h"

#define set_run_speed "run_speed"
#define sec_run_speed "run_speed"
#define set_run_speed_dude "dude"
#define set_run_speed_party "party"

#define fps_offset 4

variable enabled;

procedure start;
procedure set_run_fps(variable model, variable new_fps);

procedure start begin

  if game_loaded then begin
    variable fs_override = check_filesystem_override;
    enabled = fo2tweaks_setting(sec_main, set_run_speed);
    if enabled == 1 and fs_override then begin
      variable frm, frms, fps, path;
      variable dude = fo2tweaks_setting(sec_run_speed, set_run_speed_dude);
      variable party = fo2tweaks_setting(sec_run_speed, set_run_speed_party);
      ndebug("initialized");
      if dude then begin
        ndebug("dude enabled");
        fps = 20; // same as no armor fps
        frms = [ // vanilla PC
          "hanpwr",
          "hapowr",
          "harobe",
          "hfcmbt",
          "hfjmps",
          "hflthr",
          "hfmaxx",
          "hfmetl",
          "hfprim",
          "hmcmbt",
          "hmjmps",
          "hmlthr",
          "hmmaxx",
          "hmmetl",
          "hmwarr"
        ];
        foreach frm in frms begin
          path = "art\\critters\\" + frm;
          call set_run_fps(path, fps);
        end

/* Not working. Maybe it doesn't need to work, and engine sees them as standard hfjmps?
        frms = [ // hero appearance
          "art\\critters\\_hfjmps",
          "art\\critters\\_hflthr",
          "art\\critters\\_hfmaxx",
          "art\\critters\\_hfmetl",
          "art\\critters\\_hfprim"
        ];
        foreach frm in frms begin
          //path = "appearance\\" + frm;
          path = frm;
          call set_run_fps(path, fps);
        end
*/
      end

      if party then begin
        ndebug("party enabled");
        frms = [
          // vanilla
          "macybr", //cyberdog, k-9
          "maddog", //dogmeat
          "marobe", //goris
          "mamtn2", //marcus
          "nmfatt", //vic
          "nmmyrn", //myron
          "nmlthr", //cassidy
          "navgul", //lenny
          "nmwarr", //sulik
          //new RP frms without AT anims
          "malieu", //marcus RP armor
          "laghul"  //lenny RP new
        ];
        foreach frm in frms begin
          path = "art\\critters\\" + frm;
          call set_run_fps(path, fps);
        end

        // skynet is really slow, handling separately
        call set_run_fps("art\\critters\\marobo", 40);
      end

    end
  end
end

procedure set_run_fps(variable path, variable new_fps) begin
  variable file;
  variable path_ab = path + "ab.frm";
  variable path_at = path + "at.frm";
  file = fs_copy(path_at, path_at);
  if file == -1 then begin
    ndebug(path_at + " is missing, copying from " + path_ab);
    file = fs_copy(path_at, path_ab);
  end
  if file == -1 then begin
    ndebug("warning: couldn't find or create " + path_at);
    return;
  end
  fs_seek(file, fps_offset);
  fs_write_short(file, new_fps);
  ndebug("set fps to " + new_fps + " in " + path_at);
end
