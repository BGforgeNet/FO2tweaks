#define NAME "gl_g_auto_reload"
#include "headers/fo2tweaks/fo2tweaks.h"
#include "headers/party_orders/party_orders.h"

#define set_auto_reload "auto_reload"
variable old_game_mode := 0, new_game_mode;
variable enabled;

procedure auto_reload begin
  variable begin
  	i;
  	weapon := 0;
  	ammopid := 0;
  	list;
  	item;
  	ammo;
  	caliber;
  	magsize;
  	who := dude_obj;
  end

  new_game_mode := get_game_mode;
  ndebug("old mode " + old_game_mode + ", new mode " + new_game_mode);

  if in_combat then begin //don't do anything in combat
    ndebug("in combat, pass");
    old_game_mode := new_game_mode; //track current mode for next invocation
    return;
  end

  if old_game_mode bwand COMBAT == 0 then begin //if not coming from combat mode then skip
    ndebug("not coming from combat, pass");
    old_game_mode := new_game_mode;
    return;
  end

  old_game_mode := new_game_mode;

  i := 1;
  for (i := 1; i <= 2; i++) begin //right, left hands
		weapon := critter_inven_obj2(who, i);

		if obj_pid(weapon) == PID_SOLAR_SCORCHER and get_light_level == 65536 then begin
			ndebug("solar scorcher: 100% light, reloading");
			magsize := get_proto_data(obj_pid(weapon), PROTO_WP_MAG_SIZE);
			set_weapon_ammo_count(weapon, magsize);
			continue;
		end

		if not weapon or not is_weapon(weapon) then continue;
		ammopid := get_weapon_ammo_pid(weapon);
		if ammopid == -1 then continue;

		caliber := get_proto_data(obj_pid(weapon), PROTO_WP_CALIBER);
		magsize := get_proto_data(obj_pid(weapon), PROTO_WP_MAG_SIZE);

		ndebug("found weapon " + obj_name(weapon) + " with caliber " + caliber + ", ammo pid " + ammopid);
		ndebug("magsize is " + magsize + " ammo count " + get_weapon_ammo_count(weapon));

		if magsize > get_weapon_ammo_count(weapon) then begin
      ammo := obj_carrying_pid_obj(who, ammopid);
			if ammo then begin
				ndebug("found inven ammo pid " + ammopid);
				// first, unload existing ammo
				call inc_ammo_count(who, ammopid, get_weapon_ammo_count(weapon));
				// then, load new ammo
				set_weapon_ammo_pid(weapon, ammopid);
				if (actual_ammo_count(who, ammo) < magsize) then magsize := actual_ammo_count(who, ammo);
				set_weapon_ammo_count(weapon, magsize);
				call inc_ammo_count(who, ammopid, - magsize);
				reg_anim_clear(who);
				animate_stand_obj(who);
			end else ndebug("not found :(");
		end
  end
  intface_redraw;
end

procedure start begin
  if game_loaded then begin
    enabled := fo2tweaks_setting(sec_main, set_auto_reload);
    if enabled == 1 then begin
      ndebug("initialized");
      register_hook_proc(HOOK_GAMEMODECHANGE, auto_reload);
    end
  end
end
