#define SCRIPT_REALNAME "gl_g_map_hotkey"
#include "headers/fo2tweaks/fo2tweaks.h"
#include "headers/rp/modoc.h"

#define set_worldmap_key "worldmap_key"
#define set_townmap_key "townmap_key"

procedure start;
procedure keypress_hook_handler;
variable worldmap_key;
variable townmap_key;

procedure blowing_shitter begin
  if cur_map_index == MAP_MODOC_DOWNTHESHITTER and not shitter_has_blown then begin
    float_msg(dude_obj, "Nah... I think I'm gonna take the ladder", FLOAT_MSG_NORMAL);
    ndebug("blowing shitter, pass");
    return true;
  end
  return false;
end

procedure keypress_hook_handler begin
  variable pressed := get_sfall_arg;
  variable key := get_sfall_arg;
  if not pressed then return; //released
  if get_game_mode then return; //non-zero mode

  if worldmap_key and hotkey_pressed_now(worldmap_key, key) then begin
    if blowing_shitter then return;
    ndebug("key pressed, mode valid - calling world map");
    world_map;
  end

  if townmap_key and hotkey_pressed_now(townmap_key, key) then begin
    if blowing_shitter then return;
    ndebug("key pressed, mode valid - calling town map");
    world_map;
    tap_key(DIK_T);
  end
end

procedure start begin
  if game_loaded then begin
    worldmap_key := fo2tweaks_setting(sec_main, set_worldmap_key);
    townmap_key := fo2tweaks_setting(sec_main, set_townmap_key);
    if worldmap_key or townmap_key then begin
      ndebug("initialized, worldmap key is " + worldmap_key + ", town map key is " + townmap_key);
      register_hook_proc(HOOK_KEYPRESS, keypress_hook_handler);
    end
  end
end
