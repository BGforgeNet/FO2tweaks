#define SCRIPT_REALNAME "gl_g_map_hotkey"
#include "headers/fo2tweaks/fo2tweaks.h"
#include "headers/rp/modoc.h"

#define set_worldmap_key "worldmap_key"
#define set_townmap_key "townmap_key"

procedure start;
procedure keypress_hook_handler;
variable worldmap_key;
variable townmap_key;

procedure blowing_shitter begin
  if cur_map_index == MAP_MODOC_DOWNTHESHITTER and not shitter_has_blown then begin
    variable msg = add_extra_msg_file("g_map_hotkey.msg");
    variable scenery = list_as_array(LIST_SCENERY);
    variable s, really_in_shitter = false; // to support games other than F2 itself
    foreach s in scenery begin
      if get_script(s) == SCRIPT_MISHTRKS then really_in_shitter = true;
      break;
    end
    if really_in_shitter then begin
      float_msg(dude_obj, message_str_game(msg,1), FLOAT_MSG_NORMAL);
      ndebug("blowing shitter, pass");
      return true;
    end else begin
      ndebug("not really in shitter, pass");
      return false;
    end
  end
  return false;
end

procedure keypress_hook_handler begin
  variable pressed = get_sfall_arg;
  variable key = get_sfall_arg;
  if not pressed then return; //released

  // allow to use the same town map key on world map
  if in_world_map and hotkey_pressed_now(townmap_key, key) then begin
    tap_key(DIK_T);
    return;
  end

  if get_game_mode then return; //non-zero mode

  if worldmap_key and hotkey_pressed_now(worldmap_key, key) then begin
    if blowing_shitter then return;
    ndebug("key pressed, mode valid - calling world map");
    world_map;
  end

  if townmap_key and hotkey_pressed_now(townmap_key, key) then begin
    if blowing_shitter then return;
    ndebug("key pressed, mode valid - calling town map");
    world_map;
    tap_key(DIK_T);
  end

end

procedure start begin
  if game_loaded then begin
    worldmap_key = fo2tweaks_string(sec_main, set_worldmap_key);
    worldmap_key = char_to_dik(worldmap_key);
    townmap_key = fo2tweaks_string(sec_main, set_townmap_key);
    townmap_key = char_to_dik(townmap_key);
    if worldmap_key == DIK_0 then begin // "0"
      worldmap_key = 0;
    end
    if (townmap_key == DIK_0) or (townmap_key == DIK_T) then begin
      townmap_key = 0;
    end
    if worldmap_key or townmap_key then begin
      if worldmap_key then ndebug("worldmap key is " + worldmap_key);
      if townmap_key then ndebug("townmap key is " + townmap_key);
      else ndebug("townmap key is default = T");
      ndebug("initialized");
      register_hook_proc(HOOK_KEYPRESS, keypress_hook_handler);
    end
  end
end
