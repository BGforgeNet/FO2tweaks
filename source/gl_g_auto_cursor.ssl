#define SCRIPT_REALNAME "gl_g_auto_cursor"

#include "headers/fo2tweaks/fo2tweaks.h"

procedure start;
variable who_tile = 0;
variable last_who = 0;

procedure update_cursor begin
  variable who;
  if not in_combat then return;

  who = obj_under_cursor(true, false);
  ndebug("who is " + who);
  if who and not is_dead(who) then begin
    ndebug("who_name is " + obj_name(who));
    last_who = who;
    who_tile = tile_num(last_who);
    if get_cursor_mode == CURSOR_MOVEMENT then begin
      ndebug("who: movement to target");
      set_cursor_mode(CURSOR_TARGETING);
    end
    return;
  end

  if tile_under_cursor == who_tile then begin
    ndebug("same tile, keeping target cursor");
    if get_cursor_mode == CURSOR_MOVEMENT then begin
      ndebug("tile: movement to target");
      set_cursor_mode(CURSOR_TARGETING);
    end
    return;
  end

  if (not who or is_dead(who))
    and (get_cursor_mode == CURSOR_TARGETING)
    and not tile_is_blocked(who_tile, dude_elevation)
  then begin
    who_tile = 0;
    ndebug("no critter, tile changed");
    set_cursor_mode(CURSOR_MOVEMENT);
  end
end

procedure to_combat begin
  variable old_mode = get_sfall_arg_at(1);
  if mode_started(COMBAT, old_mode, get_game_mode) then begin
    set_cursor_mode(CURSOR_TARGETING);
    ndebug("combat started");
    return;
  end
end

procedure start begin
  if game_loaded then begin
    set_global_script_repeat(5);
    register_hook_proc(HOOK_GAMEMODECHANGE, update_cursor);
//    register_hook_proc(HOOK_GAMEMODECHANGE, to_combat);
    ndebug("enabled");
  end else begin
    call update_cursor;
  end
end
