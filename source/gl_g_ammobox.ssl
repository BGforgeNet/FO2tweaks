#define SCRIPT_REALNAME "gl_g_ammobox"
#include "headers/fo2tweaks/fo2tweaks.h"

#define set_ammobox "ammobox"
#define sec_ammobox "ammobox"
#define set_ammobox_number "number"
#define set_ammobox_color "color"

variable box_num;
variable box_color;

procedure start;
procedure update_box(variable show_new);
procedure gamemode_hook;
procedure apcost_hook;

procedure start begin
  if game_loaded then begin
    variable enabled = fo2tweaks_setting(sec_main, set_ammobox);
    box_num = fo2tweaks_setting(sec_ammobox, set_ammobox_number);
    box_color = fo2tweaks_setting(sec_ammobox, set_ammobox_color);
    if enabled == 1 then begin
      register_hook_proc(HOOK_GAMEMODECHANGE, gamemode_hook);
      register_hook_proc(HOOK_CALCAPCOST, apcost_hook);
      ndebug("initialized");
    end
  end
end

procedure apcost_hook begin
  variable who = get_sfall_arg;
  if who == dude_obj then begin
    ndebug("apcost");
    call update_box(1);
  end
end

//combat start/stop
procedure gamemode_hook begin
  variable old_mode = get_sfall_arg_at(1);
  variable new_mode = get_game_mode;
  if (old_mode bwand COMBAT) != (new_mode bwand COMBAT) then begin
    call update_box(1);
    ndebug("update box - gamemode combat");
    return;
  end
end

//some hooks are run before actual change
//show_new = 0 shows currently active ammo
//show_new = 1 shows currently inactive ammo which will become active
procedure update_box(variable show_new) begin
  variable item, weapon, ammo_pid, ammo_label;
  if not in_combat then begin
    if is_iface_tag_active(box_num) then begin
      hide_iface_tag(box_num);
    end
    return;
  end

  // in combat
  if show_new == 1 then begin
    ammo_pid = get_active_ammo_pid(dude_obj);
    weapon = get_active_weapon(dude_obj);
  end else begin
    ammo_pid = get_dude_inactive_ammo_pid;
    weapon = get_dude_inactive_weapon;
  end
  if ammo_pid and is_gun(weapon) then begin
    ammo_label = message_str_game(GAME_MSG_PRO_ITEM, ammo_pid * 100);
    set_iface_tag_text(box_num, ammo_label, box_color);
    if not is_iface_tag_active(box_num) then begin
      show_iface_tag(box_num);
    end
  end else begin
    if is_iface_tag_active(box_num) then begin
      hide_iface_tag(box_num);
    end
  end

end
