#define SCRIPT_REALNAME "gl_g_no_scope_penalty"
#include "headers/fo2tweaks/fo2tweaks.h"

#define set_no_scope_penalty "no_scope_penalty"
#define sec_no_scope_penalty "no_scope_penalty"
#define set_scope_remove "remove"
#define set_scope_to_long "to_long"


variable enabled;

procedure remove_scope;

procedure start begin
  if game_loaded then begin
    enabled = fo2tweaks_setting(sec_main, set_no_scope_penalty);
    if enabled == 1 then call remove_scope;
  end
end

procedure map_enter_p_proc begin
  if enabled == 1 then call remove_scope;
end

procedure remove_scope begin
  variable key, pid, pids, old_range, new_range, perk, ap, ap2, burst;
  ndebug("initializing");

  //vanilla scoped rifle
  pids = fo2tweaks_comsep_setting(sec_no_scope_penalty, set_scope_remove);
  foreach pid in pids begin
    set_proto_data(pid, PROTO_WP_PERK, -1); //remove scope range
    old_range = get_proto_data(pid, PROTO_WP_RANGE_1);
    new_range = floor2(old_range * 1.2);
    set_proto_data(pid, PROTO_WP_RANGE_1, new_range);
    ndebug("removed scope range from pid " + pid + " " + typeof(pid) + ", old range " + old_range + " new range " + new_range);
  end

    //EcCo: set long_range for bozar and sniper rifle
  pids = fo2tweaks_comsep_setting(sec_no_scope_penalty, set_scope_to_long);
  foreach pid in pids begin
    perk = get_proto_data(pid, PROTO_WP_PERK);
    burst = get_proto_data(pid, PROTO_WP_BURST);
    ap2 = get_proto_data(pid, PROTO_WP_APCOST_2);
    ndebug("pid " + pid + " burst " + burst + " ap2 " + ap2 + " perk " + perk);
    if perk != PERK_weapon_long_range
      and not (burst > 1)
      and ap2 == 0
    then begin
      set_proto_data(pid, PROTO_WP_PERK, PERK_weapon_long_range);
      ndebug("added long range to pid " + pid);
    end
  end

  intface_redraw;
  ndebug("initialized");
end
