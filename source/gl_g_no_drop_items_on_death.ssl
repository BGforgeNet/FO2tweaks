#define SCRIPT_REALNAME "gl_g_no_drop_items_on_death"

#include "headers/fo2tweaks/fo2tweaks.h"

variable box := 0;
variable critter := 0;

#define SFVGAR_BOX "g_dh_box"
#define SFVGAR_CRITTER "g_dh_cri"

procedure deathanim2_handler;
procedure ondeath_handler;

procedure start begin
  if game_loaded then begin
    variable enabled := fo2tweaks_setting(sec_main, "no_drop_items_on_death");
    if enabled == 1 then begin
      register_hook_proc(HOOK_DEATHANIM2, deathanim2_handler);
      register_hook_proc(HOOK_ONDEATH, ondeath_handler);
      ndebug("initialized");
    end
  end
end

procedure deathanim2_handler begin
  variable item_pid := get_sfall_arg;
  variable attacker := get_sfall_arg;
  variable target := get_sfall_arg;
  variable damage_amount := get_sfall_arg;
  variable death_anim_id := get_sfall_arg;
  if ( (death_anim_id == ANIM_electrified_to_nothing or death_anim_id == ANIM_exploded_to_nothing)
      and is_critter(target) )
  then begin
    box := create_object_sid(PID_FOOTLOCKER_CLEAN_LEFT, 0, 0, -1);
    set_sfall_global(SFVGAR_BOX, box);
    move_obj_inven_to_obj(target, box);
    set_sfall_global(SFVGAR_CRITTER, target);
  end
end

procedure ondeath_handler begin
  critter := get_sfall_arg;
  if get_sfall_global_int(SFVGAR_CRITTER) == critter then begin
    move_obj_inven_to_obj(get_sfall_global_int(SFVGAR_BOX), critter);
    destroy_object(get_sfall_global_int(SFVGAR_BOX));
    set_sfall_global(SFVGAR_BOX, 0);
    set_sfall_global(SFVGAR_CRITTER, 0);
  end
end
