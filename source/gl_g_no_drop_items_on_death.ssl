#define SCRIPT_REALNAME "gl_g_no_drop_items_on_death"

#include "headers/fo2tweaks/fo2tweaks.h"

variable critters_boxes;

procedure deathanim2_handler;
procedure ondeath_handler;

procedure start begin
  if game_loaded then begin
    variable enabled = fo2tweaks_setting(sec_main, "no_drop_items_on_death");
    if enabled == 1 then begin
      register_hook_proc(HOOK_DEATHANIM2, deathanim2_handler);
      register_hook_proc(HOOK_ONDEATH, ondeath_handler);
      critters_boxes = create_array_map;
      fix_array(critters_boxes);
      ndebug("initialized");
    end
  end
end

procedure deathanim2_handler begin
  variable item_pid = get_sfall_arg;
  variable attacker = get_sfall_arg;
  variable target = get_sfall_arg;
  variable damage_amount = get_sfall_arg;
  variable death_anim_id = get_sfall_arg;

  // shouldn't ever fire, but just in case
  if not is_critter(target) then return;

  // Only `ANIM_electrified_to_nothing` and `ANIM_exploded_to_nothing` will drop everything on the ground.
  // https://github.com/alexbatalov/fallout2-re/blob/b135fc46ef40c4aecd156f3cebcf88ec531bb8ac/src/game/actions.c#L463
  if (death_anim_id == ANIM_electrified_to_nothing) or (death_anim_id == ANIM_exploded_to_nothing) then begin
    variable box = create_object_sid(PID_FOOTLOCKER_CLEAN_LEFT, 0, 0, -1);
    move_obj_inven_to_obj(target, box);
    critters_boxes[target] = box;
    ndebug("moved inventory from " + obj_name(target) + " to temp box");
  end
end

procedure ondeath_handler begin
  variable critter = get_sfall_arg;
  variable box = critters_boxes[critter];
  if box then begin
    move_obj_inven_to_obj(box, critter);
    destroy_object(box);
    critters_boxes[critter] = 0;
    ndebug("moved inventory from temp box back to " + obj_name(critter));
  end
end
