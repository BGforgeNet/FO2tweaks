#define SCRIPT_REALNAME "gl_g_party_level_match"
#include "headers/fo2tweaks/fo2tweaks.h"

#define set_party_level_match "party_level_match"
#define sec_party_level_match "party_level_match"
#define set_party_finlevel "final_level"
#define set_party_3stages_start "3stages_start"
#define set_party_4stages_start "4stages_start"
#define set_party_5stages_start "5stages_start"
#define set_party_6stages_start "6stages_start"
#define sec_party_level_pids "party_level_maxstage"

variable begin
  enabled;
  fin_level;
  step;
  stage3_start;
  stage4_start;
  stage5_start;
  stage6_start;
  pid_maxstage;
end

procedure levelup begin
  variable
    max_stage,
    pc_level,
    cur_stage,
    start_level,
    party_names,
    expected_stage;

  if enabled and true_party_size > 0 then begin
    variable who;
    pc_level := get_pc_stat(PCSTAT_level);

    foreach who in party_member_list_critters begin
      variable pid := obj_pid(who);
      cur_stage := get_npc_level(pid);
      max_stage := pid_maxstage[pid];

      switch max_stage begin
        case 3:
          start_level := stage3_start;
        case 4:
          start_level := stage4_start;
        case 5:
          start_level := stage5_start;
        default:
          start_level := stage6_start;
      end

      //already maxed or too early
      if cur_stage >= max_stage or pc_level <= start_level then continue;

      step := floor2((fin_level - start_level)/max_stage);

      ndebug("npc is " + obj_name(who));
      ndebug("cur_stage = " + cur_stage);
      ndebug("pc_level = " + pc_level);
      ndebug("start_level = " + start_level);
      ndebug("step = " + step);

      expected_stage := floor2((pc_level - start_level)/step);
      ndebug("expected_stage = " + expected_stage);
      ndebug("max_stage = " + max_stage);

      if cur_stage < expected_stage then begin
        ndebug("leveling up " + obj_name(who));
        inc_npc_level(pid);
      end

    end
  end
end

procedure gamemode_handler begin
  variable old_mode := get_sfall_arg_at(1);
  variable new_mode := get_game_mode;
  if mode_ended(DIALOG, old_mode, new_mode) then call levelup;
end

procedure start begin
  if game_loaded then begin
    enabled := fo2tweaks_setting(sec_main, set_party_level_match);
    if enabled then begin
      variable  tmp;
      fin_level := fo2tweaks_setting(sec_party_level_match, set_party_finlevel);
      stage3_start := fo2tweaks_setting(sec_party_level_match, set_party_3stages_start);
      stage4_start := fo2tweaks_setting(sec_party_level_match, set_party_4stages_start);
      stage5_start := fo2tweaks_setting(sec_party_level_match, set_party_5stages_start);
      stage6_start := fo2tweaks_setting(sec_party_level_match, set_party_6stages_start);

      //load default, override from ini
      tmp := f2rp_pid_maxstage;
      pid_maxstage := get_ini_section_ints(fo2tweaks_ini, sec_party_level_pids);
      pid_maxstage := array_append(tmp, pid_maxstage);
      fix_array(pid_maxstage);

      register_hook_proc(HOOK_GAMEMODECHANGE, gamemode_handler);
      ndebug("initialized");
    end
  end
end
